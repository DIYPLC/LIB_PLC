FUNCTION_BLOCK FbReset
TITLE = 'FbReset'
//Флаг первого скана с TOF задержкой CFC PCS7.

{//FB attributes for PCS7.
S7_tasklist := 'OB100' //Call in OB100
}

VERSION : '1.1'
AUTHOR  : VA
FAMILY  : LIB_PLC

VAR_INPUT
TimeDelayOff 
{//Атрибуты переменных PCS7.
S7_link    := 'false'; //'false' запрет CFC соединений.
S7_dynamic := 'true'   //'true' просмотр в CFC.
} : DINT := 2000; //Задержка выключен. импульса сброса ms.
END_VAR

VAR_OUTPUT
Reset :BOOL := FALSE; //Сброс алгоритма.
END_VAR

VAR
RST :BOOL := FALSE; //Сброс алгоритма локальный.
TOF1 :TOF; //TOF timer.
END_VAR 

VAR_TEMP
TOP_SI: STRUCT
    EV_CLASS :BYTE ;
    EV_NUM   :BYTE ;
    PRIORITY :BYTE ;
    NUM      :BYTE ;
    TYP2_3   :BYTE ;
    TYP1     :BYTE ;
    ZI1      :WORD ;
    ZI2_3    :DWORD;
END_STRUCT;
START_UP_SI: STRUCT
    EV_CLASS :BYTE ;
    EV_NUM   :BYTE ;
    PRIORITY :BYTE ;
    NUM      :BYTE ;
    TYP2_3   :BYTE ;
    TYP1     :BYTE ;
    ZI1      :WORD ;
    ZI2_3    :DWORD;
END_STRUCT;
ERROR_RD_SINFO :INT;
END_VAR

//Анализ из какого OB произошел вызов.
//Штатно вызов из OB35 или другой цикличный.
//При перезагрузке вызов из OB100.
ERROR_RD_SINFO := RD_SINFO(TOP_SI := TOP_SI, START_UP_SI := START_UP_SI); // SFC6
RST := (TOP_SI.NUM = 100); //Call in OB100

//Задержка выключен. импульса первого скана.
IF (TimeDelayOff <= 0) THEN
TOF1.IN := FALSE;
TOF1.PT := T#0ms;
TOF1();
Reset := RST;
ELSE
TOF1.IN := RST;
TOF1.PT := DINT_TO_TIME(TimeDelayOff);
TOF1(); 
Reset := TOF1.Q; // OUT: BOOL
END_IF;

END_FUNCTION_BLOCK

//Блок спроектирован под PCS7 v5.

// @COPYLEFT ALL WRONGS RESERVED :)
// Author: VA
// Contacts: DIY.PLC.314@gmail.com
// Date start LIB_PLC: 2014
// License: GNU GPL-2.0-or-later
// https://www.gnu.org/licenses/old-licenses/gpl-2.0.html
// https://www.youtube.com/watch?v=n1F_MfLRlX0
// https://www.youtube.com/@DIY_PLC
// https://github.com/DIYPLC/LIB_PLC
// https://oshwlab.com/diy.plc.314/PLC_HW1_SW1
// https://3dtoday.ru/3d-models/mechanical-parts/body/korpus-na-din-reiku
// https://t.me/DIY_PLC
