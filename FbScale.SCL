FUNCTION_BLOCK FbScale //Масштабирование InMin...InMax -> OutMin...OutMax.
TITLE = 'Масштабирование InMin...InMax -> OutMin...OutMax.'

//       DbScale
//    +-----------+
//    |  FbScale  |
// ->-|In      Out|->-
//   -|InMax      |
//   -|InMin      |
//   -|OutMax     |
//   -|OutMin     |
//    +-----------+

{//Атрибуты для PCS7.
S7_read_back := 'true' ; //CFC: Chart>Readback активен для прототипов DB.
S7_blockview := 'big'    //CFC: отображение блока маленькое или большое.
}

//Атрибуты для STEP7.
VERSION : '2.1'
AUTHOR  : VA
FAMILY  : LIB_PLC

VAR_INPUT //Входные переменные, сохраняемые.
In     :REAL:= 0.0    ; //Вход до масштабирования.
InMax  :REAL:= 27648.0; //Максимум входного диапазона.
InMin  :REAL:= 0.0    ; //Минимум входного диапазона.
OutMax :REAL:= 20.0   ; //Максимум выходного диапазона.
OutMin :REAL:= 4.0    ; //Минимум выходного диапазона.
END_VAR

VAR_OUTPUT //Выходные переменные, сохраняемые.
Out :REAL := 0.0; //Выход после масштабирования.
END_VAR

//Масштабирование сигнала.
IF (InMax = InMin) //Деление на 0 может остановить процессор.
THEN
//При ошибке исходных данных.
Out := 0.0;
ELSE
//Масштабирование.
Out := (OutMax - OutMin) * ((In - InMin)/(InMax - InMin)) + OutMin;
END_IF;

END_FUNCTION_BLOCK

// Статическая характеристика.
//
//       ^ Out
//       |
// OutMax|<---------------*
//       |              * |
//       |            *   |
//    Out|<---------*     |
//       |        * |     |
//       |      *   |     |
// OutMin|<---*     |     |
//       |    |     |     |
//       |    |     |     |
//       +---------------------> In
//          InMin  In   InMax

// Выход сигнал
// Out = k * In + b
//
// Для расчета k, b надо решить систему двух линейных уравнений с двумя неизвестными.
// {OutMax = k * InMax + b
// {OutMin = k * InMin + b
//
// Усиление:
//      OutMax - OutMin 
// k = -----------------
//       InMax - InMin  
//
// Смещение:
// b = OutMax - k * InMax
//
// После подстановки в уравнение Out = k * In + b констант k, b,
// и сокращения числа арифметических операций до минимума получаем:
// Out := (OutMax - OutMin) * ((In - InMin)/(InMax - InMin)) + OutMin;

// Данное линейное преобразование на практике легко вывести через промежуточный диаппазон 0...1:
// InMin...InMax -> 0...(InMax-InMin) -> 0...1 -> 0...(OutMax-OutMin) -> OutMin...OutMax.
// Пример рассчета давления с датчика  4...20мА -> 0...10Бар.
// 4...20 -  4 = 0...16
// 0...16 / 16 = 0...1
// 0...1  * 10 = 0...10
// При токе 12мА
// 12  -   4 = 8
// 8   /  16 = 0.5
// 0.5 *  10 = 5Бар
// Давление 5Бар.

//  +---------+
//  | GNU GPL |
//  +---------+
//  |
//  |
//  .= .-_-. =.
// ((_/)o o(\_))
//  `-'(. .)`-'
//  |/| \_/ |\
//  ( |     | )
//  /"\_____/"\
//  \__)   (__/
// @COPYLEFT ALL WRONGS RESERVED :)
// Author: VA
// Contacts: DIY.PLC.314@gmail.com
// License: GNU GPL v2
