FUNCTION_BLOCK Fb2PosControl //Регулятор двухпозиционный.

//       Db2PosControl
//     +---------------+
//     | Fb2PosControl |
//  ->-|PV           OP|->-
//    -|SP             |
//    -|HYST           |
//     +---------------+

//Атрибуты для STEP7.
TITLE   = 'Регулятор двухпозиционный.'
VERSION : '2.0'
AUTHOR  : 'VA'
FAMILY  : 'LibPlc'
{//Атрибуты для PCS7.
S7_read_back := 'true' ; //CFC: Chart>Readback активен для прототипов DB.
S7_blockview := 'big'  ; //CFC: отображение блока мелнькое или большое.
//Опции компилятора SCL.
GenerateReferenceData := 'y' ; //SCL: Генерировать перекрестные ссылки.
CreateDebugInfo       := 'y' ; //SCL: Генерировать оладочную информацию.
SetOKFlag             := 'y' ; //SCL: ENO = 1
MonitorArrayLimits    := 'y' ; //SCL: Следить за границами массивов.
OptimizeObjectCode    := 'y'   //SCL: Оптимизация объектного кода.
}

VAR_INPUT //Входные переменные, сохраняемые.
PV   :REAL := 0.0; //Измеренное значение регулируемого параметра.
SP   :REAL := 0.0; //Заданное значение регулируемого параметра.
HYST :REAL := 5.0; //Гистерезис > 0.
END_VAR

VAR_OUTPUT //Выходные переменные, сохраняемые.
OP :BOOL := FALSE; //Выход управляющего сигнала.
END_VAR

VAR //Внутренние переменные, сохраняемые.
ER :REAL := 0.0; //Ошибка регулирования.
END_VAR

//Ошибка регулирования.
ER := SP - PV;

//Регулятор двухпозиционный.
IF (ER >= HYST)
THEN
OP:=TRUE;
END_IF;

IF (ER <= HYST * (-1.0))
THEN
OP := FALSE;
END_IF;

END_FUNCTION_BLOCK

//Применение.
//
//Двухпозиционный регулятор используется как простейший регулятор температуры, давления, уровня.

// Условное графическое обозначение.
//
//      +---------------+
//      |               |
//      |    +--<--+--  |
//      |    |     |    |
// ER->-|    v     ^    |->-OP
//      |    |     |    |
//      |  --+-->--+    |
//      |               |
//      +---------------+

// Статическая характеристика.
//
//                 OP
//                  ^
//                  |
//           ***<***1***<******
//           *      |      *
//           v      |      ^
//           *      |      *
// -ER <--******>***0***>***--> +ER
//           |      |      |
//           |<---->|<---->|
//             HYST   HYST

// Временная характеристика.
//
// ^ PV,SP
// |         PV
// |----------*---------------*------
// |         * *             * *   |
// |        *   *           *   *  |  SP + HYST
// |       *     *         *       |
// |======*=======*=======*========== SP
// |     *         *     *         |
// |    *           *   *          |  SP - HYST
// |   *             * *           |
// |--*---------------*--------------
// | * 
// |*
// +-----------------------------------------> t[s]
//
// ^ OP
// |
// 1***********       *********
// |          *       *       *
// 0----------*********-------***------------> t[s]

// Схема включения.
//
//                  +---------------+
//                  |               |   Объект управления
//                  |    +--<--+--  |     +-----------+
//        +---+ ER  |    |     |    | OP  |     K     |
// SP-->--| + |-->--|    v     ^    |-->--| --------- |-->--+-->PV
// PV  +--| - |     |    |     |    |     | T * s + 1 |     |
//     |  +---+     |  --+-->--+    |     +-----------+     |
//     |            |               |                       |
//     |            +---------------+                       |
//     |                                                    |
//     +----------------------------------------------------+

//  +---------+
//  | GNU GPL |
//  +---------+
//  |
//  |
//  .= .-_-. =.
// ((_/)o o(\_))
//  `-'(. .)`-'
//  |/| \_/ |\
//  ( |     | )
//  /"\_____/"\
//  \__)   (__/
// @COPYLEFT ALL WRONGS RESERVED :)
// Author: VA
// Contacts: DIY.PLC.314@gmail.com
// License: GNU GPL v2
