FUNCTION_BLOCK Fb_Ts_ms //Рассчет шага дискретизации по времени от RTC.

//          Db_Ts_ms
//    +-----------------+
//    |     Fb_Ts_ms    |
// ->-|Seconds     Ts_ms|->-
// ->-|Millisecond    Ts|->-
// ->-|Reset            |
//    +-----------------+

//Атрибуты для STEP7.
TITLE   = 'Рассчет шага дискретизации по времени от RTC.'
VERSION : '2.0'
AUTHOR  : 'VA'
FAMILY  : 'SIMATIC'
{//Атрибуты для PCS7.
S7_read_back := 'true' ; //CFC: Chart>Readback активен для прототипов DB.
S7_blockview := 'big'  ; //CFC: отображение блока мелнькое или большое.
//Опции компилятора SCL.
GenerateReferenceData := 'y' ; //SCL: Генерировать перекрестные ссылки.
CreateDebugInfo       := 'y' ; //SCL: Генерировать оладочную информацию.
SetOKFlag             := 'y' ; //SCL: ENO = 1
MonitorArrayLimits    := 'y' ; //SCL: Следить за границами массивов.
OptimizeObjectCode    := 'y'   //SCL: Оптимизация объектного кода.
}

VAR_INPUT //Входные переменные, сохраняемые.
Seconds     :INT  := 0;     //Текущее время- Секунда 0...59.
Millisecond :INT  := 0;     //Текущее время- Миллисекунды 0...999.
Reset       :BOOL := FALSE; //Сброс при перезагрузке.
END_VAR

VAR_OUTPUT //Выходные переменные, сохраняемые.
Ts_ms :DINT  := 0;   //Шаг дискретизации по времени [мс].
Ts    :REAL := 0.0; //Шаг дискретизации по времени [с].
END_VAR

VAR //Внутренние переменные, сохраняемые.
Time_ms_previous :DINT := 0; //RTC предидущее значение 0...59999ms.
END_VAR

VAR_TEMP  //Временные переменные, не сохраняемые.
Time_ms_current  :DINT; //RTC 0...59999ms.
END_VAR

//RTC 0...59999ms
Time_ms_current := (INT_TO_DINT(Seconds) * 1000) + INT_TO_DINT(Millisecond);

//Инициализируем время при перезагрузке.
IF (Reset) THEN
Time_ms_previous := Time_ms_current;
END_IF;

//Разность времени с учетом переполнения.
IF (Time_ms_current >= Time_ms_previous) THEN
Ts_ms := Time_ms_current - Time_ms_previous;
//Ts_ms = 7 - 2 = 5 ms
//0
//1
//2 <- Time_ms_previous
//3
//4
//5
//6
//7 <- Time_ms_current
//8
//9
ELSE
Ts_ms := (60000 - Time_ms_previous) + Time_ms_current;
//Ts_ms = (60000 - 59997) + 2 = 5 ms
//59995
//59996
//59997 <- Time_ms_previous
//59998
//59999
//0
//1
//2 <- Time_ms_current
//3
//4
END_IF;

//Запомнить предидущее время.
Time_ms_previous := Time_ms_current;

//Время скана не может быть больше 1000ms
//Защита на случай если ПЛК переведут в стоп на длительное время.
//Защита от внезапной корректировки времени RTC.
//Защита от неверных входных данных.
IF ((Ts_ms >= 1000) OR (Ts_ms < 0)) THEN
Ts_ms := 0;
END_IF;

//Шаг дискретизации по времени [с].
Ts := DINT_TO_REAL(Ts_ms) * 0.001;

END_FUNCTION_BLOCK

//  +---------+
//  | GNU GPL |
//  +---------+
//  |
//  |
//  .= .-_-. =.
// ((_/)o o(\_))
//  `-'(. .)`-'
//  |/| \_/ |\
//  ( |     | )
//  /"\_____/"\
//  \__)   (__/
// @COPYLEFT ALL WRONGS RESERVED :)
// Author: VA
// Contacts: DIY.PLC.314@gmail.com
// License: GNU GPL v2
