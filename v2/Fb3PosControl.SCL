FUNCTION_BLOCK Fb3PosControl //Регулятор трехпозиционный.

//       Db3PosControl
//     +---------------+
//     | Fb3PosControl |
//  ->-|PV        OpPos|->-
//    -|SP        OpNeg|->-
//    -|DBand          |
//    -|HystPos        |
//    -|HystNeg        |
//     +---------------+

VAR_INPUT //Входные переменные, сохраняемые.
PV      :REAL := 0.0; //Измеренное значение регулируемого параметра.
SP      :REAL := 0.0; //Заданное значение регулируемого параметра.
DBand   :REAL := 2.0; //Зона нечувствительности > 0.
HystPos :REAL := 1.0; //Гистерезис > 0.
HystNeg :REAL := 1.0; //Гистерезис > 0.
END_VAR

VAR_OUTPUT //Выходные переменные, сохраняемые.
OpPos :BOOL :=FALSE; //Выход управляющего сигнала положительный.
OpNeg :BOOL :=FALSE; //Выход управляющего сигнала отрицательный.
END_VAR

VAR //Внутренние переменные, сохраняемые.
ER :REAL := 0.0; //Ошибка регулирования.
END_VAR

//Ошибка регулирования.
ER := SP - PV;

//ВКЛ нагрев или движение вперед.
IF (ER >= (DBand + HystPos))
THEN
OpPos := TRUE;
END_IF;

//ВЫКЛ нагрев или движение вперед.
IF (ER <= (DBand - HystPos))
THEN
OpPos := FALSE;
END_IF;

//ВКЛ охлаждение или движение назад.
IF (ER <= (0.0 - DBand - HystNeg))
THEN
OpNeg := TRUE;
END_IF;

//ВЫКЛ охлаждение или движение назад.
IF (ER >= (0.0 - DBand + HystNeg))
THEN
OpNeg := FALSE;
END_IF;

END_FUNCTION_BLOCK

//Применение.
//
//Трехпозиционный регулятор используется как простейший регулятор температуры или положения.

// Статическая характеристика.
//
//                           OpPos
//                             ^
//                             |  DBand
//                             |<-------->|
//                             |          |
//                             |   HystPos|HystPos
//                             |   |<---->|<---->|
//                             |   |      |      |
//                            1|---***<***1***<******
//                             |   *      |      *
//                             |   v      |      ^
//                             |   *      |      *
// -ER <-----***<*******<******0******>*******>***-----> +ER
//           *      |      *   |
//           v      |      ^   |
//           *      |      *   |
//        ******>***1***>***---|-1
//           |      |      |   |
//           |<---->|<---->|   |
//           HystHeg|HystHeg   |
//                  |          |
//                  |<-------->|
//                     DBand   |
//                             v
//                           OpNeg

// Схема включения.
//
//        +---------------+     +-------------+     +-------------+
// SP-->--|SP        OpPos|-->--| Нагреватель |-->--| Датчик      |--+-->PV
//        |               |     +-------------+     | температуры |  |
//        |               |                         |             |  |
//        |               |     +-------------+     |             |  |
//     +--|PV        OpNeg|-->--| Охладитель  |-->--|             |  |
//     |  +---------------+     +-------------+     +-------------+  |
//     |                                                             |
//     +-------------------------------------------------------------+
//
//        +---------------+     +--------------+     +-------------+
// SP-->--|SP        OpPos|-->--| Мотор вперед |-->--| Датчик      |--+-->PV
//        |               |     +--------------+     | положения   |  |
//        |               |                          |             |  |
//        |               |     +--------------+     |             |  |
//     +--|PV        OpNeg|-->--| Мотор назад  |-->--|             |  |
//     |  +---------------+     +--------------+     +-------------+  |
//     |                                                              |
//     +--------------------------------------------------------------+

//  +---------+
//  | GNU GPL |
//  +---------+
//  |
//  |
//  .= .-_-. =.
// ((_/)o o(\_))
//  `-'(. .)`-'
//  |/| \_/ |\
//  ( |     | )
//  /"\_____/"\
//  \__)   (__/

