FUNCTION_BLOCK FbRamp //Рампа разгона / торможения.

//        DbRamp
//    +------------+
//    |   FbRamp   |
// ->-|In       Out|->-
//   -|TAcc        |
//   -|TDec        |
//   -|Ts          |
//    +------------+

VAR_INPUT //Входные переменные, сохраняемые.
In   :REAL := 0.0; //Входной сигнал.
TAcc :REAL := 1.0; //Время разгона на единицу [с].
TDec :REAL := 1.0; //Время торможения на единицу [c].
Ts   :REAL := 0.1; //Шаг дискретизации по времени [с].
END_VAR

VAR_OUTPUT //Выходные переменные, сохраняемые.
Out :REAL := 0.0; //Выходной сигнал.
END_VAR

VAR //Внутренние переменные, сохраняемые.
AccMode :BOOL := FALSE; //Активен режим ускорения.
DecMode :BOOL := FALSE; //Активен режим замедления.
END_VAR

AccMode := (In  > Out); //Активен режим ускорения.
DecMode := (In  < Out); //Активен режим замедления.

//Режим замедления.
IF (DecMode)
THEN
    IF (TDec <= 0.0)
    THEN
    Out := In;
    ELSE
    Out := Out - (Ts / TDec);
    END_IF;
END_IF;

//Режим ускорения.
IF (AccMode)
THEN
    IF (TAcc <= 0.0)
    THEN
    Out := In;
    ELSE
    Out := Out + (Ts / TAcc);
    END_IF;
END_IF;

//Установившийся режим.
IF ((AccMode AND (Out >= In)) OR (DecMode AND (Out <= In)) OR (In = Out))
THEN
Out := In;
END_IF;

END_FUNCTION_BLOCK

// Временная характеристика.
//
// ^ In
// |
// 1  *************
// |  *           *
// |  *           *
// |  *           *
// 0--------------------> t[s]
//
// ^ Out
// |
// |   TAcc       TDec
// |    1s        0.5s
// |  <----->     <-->
// 1        *******   
// |      *        *  
// |    *           * 
// |  *              *
// 0--------------------> t[s]

// Блок схема.
//
// Константы    Переключатель    Интегратор
// +------+     +-----------+
// |  1   |     | In > Out  |
// | ---- |-->--|--+        |
// | TAcc |     |   \       |
// +------+     |    \      |
//              |     \     |
// +------+     |      \    |     +-----+
// |      |     |       \   |     |  1  |
// |  0   |-->--|--+     +--|-->--| --- |-->-- Out
// |      |     | In ~= Out |     |  s  |
// +------+     |           |     +-----+
//              |           |
// +------+     |           |
// |  1   |     |           |
// | ---- |-->--|--+        |
// | TDec |     | In < Out  |
// +------+     +-----------+

//  +---------+
//  | GNU GPL |
//  +---------+
//  |
//  |
//  .= .-_-. =.
// ((_/)o o(\_))
//  `-'(. .)`-'
//  |/| \_/ |\
//  ( |     | )
//  /"\_____/"\
//  \__)   (__/
// @COPYLEFT ALL WRONGS RESERVED :)
// Author: VA
// Contacts: DIY.PLC.314@gmail.com
// License: GNU GPL v2
