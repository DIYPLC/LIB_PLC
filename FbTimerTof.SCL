FUNCTION_BLOCK FbTimerTof //TOF - Таймер с задержкой на выключение.

//        DbTimerTof
//    +----------------+
//    |   FbTimerTof   |
// ->-|In           Out|->-
//   -|TimeDelayOff    |
//   -|Ts_ms           |
//    +----------------+

VAR_INPUT //Входные переменные, сохраняемые.
In           :BOOL :=FALSE; //Вход таймера.
TimeDelayOff :DINT :=5000 ; //Время задержки выключения [мс].
Ts_ms        :DINT :=100  ; //Шаг дискретизации по времени [мс].
END_VAR

VAR_OUTPUT //Выходные переменные, сохраняемые.
Out :BOOL := FALSE; //Выход таймера.
END_VAR

VAR //Внутренние переменные, сохраняемые.
Timer1 :DINT := 0; //Текущее время таймер1 [мс].
END_VAR

IF (TimeDelayOff > 0) THEN //Таймер включен.
  IF (In) THEN //На входе 1 то на выходе тоже 1.
  Out := TRUE;
  Timer1 := 0;   
  ELSE //Если на входе 0.
    IF (Out) THEN //На выходе 1.       
      IF (Timer1 >= TimeDelayOff) THEN //Задержка включения закончилась.
      Out := FALSE;
      Timer1 := 0;
      ELSE //Задержка включения активна.
      Out := TRUE;
      Timer1 := Timer1 + Ts_ms;
      END_IF;
    ELSE //На выходе 0.
    Out := FALSE;
    Timer1 := 0;
    END_IF;
  END_IF;
ELSE //Таймер выключен.
Out := In;
Timer1 := 0;
END_IF;

END_FUNCTION_BLOCK

// Применение.
//
//Блок может применятся как фильтр дребезга контактов и как расширитель им-пульсов.

// Временная характеристика.
//
//  In
//  ^
//  |     TimeDelayOff          TimeDelayOff
//  |       |<--->|               |<--->|
// 1|   *****     |     ***** *** *     |
//  |   *   *     |     *   * * * *     |
// 0*****---*************---***-*************----> t[s]
//
//  Out
//  ^
//  |     TimeDelayOff          TimeDelayOff
//  |       |<--->|               |<--->|
// 1|   ***********     *****************
//  |   *         *     *               *
// 0*****---------*******---------------*****----> t[s]

//  +---------+
//  | GNU GPL |
//  +---------+
//  |
//  |
//  .= .-_-. =.
// ((_/)o o(\_))
//  `-'(. .)`-'
//  |/| \_/ |\
//  ( |     | )
//  /"\_____/"\
//  \__)   (__/
// @COPYLEFT ALL WRONGS RESERVED :)
// Author: VA
// Contacts: DIY.PLC.314@gmail.com
// License: GNU GPL v2
