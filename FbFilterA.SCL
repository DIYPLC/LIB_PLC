FUNCTION_BLOCK FbFilterA //Фильтр апериодический.

//      DbFilterA
//    +-----------+
//    | FbFilterA |
// ->-|In      Out|->-
//   -|Tf         |
//   -|Ts         |
//    +-----------+

//Атрибуты для STEP7.
TITLE   = 'Фильтр апериодический.'
VERSION : '2.0'
AUTHOR  : 'VA'
FAMILY  : 'LIB_PLC'
{//Атрибуты для PCS7.
S7_read_back := 'true' ; //CFC: Chart>Readback активен для прототипов DB.
S7_blockview := 'big'    //CFC: отображение блока маленькое или большое.
}

VAR_INPUT //Входные переменные, сохраняемые.
In :REAL := 0.0; //Входной сигнал до фильтрации.
Tf :REAL := 1.0; //Постоянная времени фильтра [с].
Ts :REAL := 0.1; //Шаг дискретизации по времени [с].
END_VAR

VAR_OUTPUT //Выходные переменные, сохраняемые.
Out :REAL := 0.0; //Выходной сигнал после фильтрации.
END_VAR

VAR_TEMP //Внутренние переменные, не сохраняемые.
Tmp :REAL; //Временная переменная вход интегратора.
END_VAR

//W(s) = 1 / (1 + Tf * s) при Ts->0.
IF (Tf <= 0.0) THEN
Out := In;
ELSE
Tmp := (In - Out) / Tf;
Out := Out + Tmp * Ts;
END_IF;

END_FUNCTION_BLOCK

// Внутренняя схема фильтра.
//
//          Вычитание   Деление      Интегрирование
//            +---+     +------+     +----------+
// In ----->--| + |     |   1  | Tmp |  Ts * z  |
//            |   |-->--| ---- |-->--| -------- |--+-->-- Out
//      +-->--| - |     |  Tf  |     |   z - 1  |  |
//      |     +---+     +------+     +----------+  |
//      |                                          |
//      +------------------------------------------+

// Условное графическое обозначение (приближенное).
//
//    +--------------+
//    |      1       |
// ->-| ------------ |->-
//    |  Tf * s + 1  |
//    +--------------+

// Электрическая схема фильтра.
//
//         R                  
// +    +-----+              +
// o----|     |----o---------o
//      +-----+    |          
//                 | C        
//               -----        
// U In          -----   U Out
//                 |          
// -               |         -
// o---------------o---------o
// Tf = R * C ([с] = [Ом] * [Ф])

// Временная характеристика.
//
// ^ Out
// |
// 1         *** Y(t) = 1 - exp(-(t/Tf))
// |      *     
// |    *       
// |  *         
// | *          
// 0---------------> t[s]
//
// ^ In
// |
// 1************ X(t) = 1
// |            
// |            
// |            
// |            
// 0---------------> t[s]

//  +---------+
//  | GNU GPL |
//  +---------+
//  |
//  |
//  .= .-_-. =.
// ((_/)o o(\_))
//  `-'(. .)`-'
//  |/| \_/ |\
//  ( |     | )
//  /"\_____/"\
//  \__)   (__/
// @COPYLEFT ALL WRONGS RESERVED :)
// Author: VA
// Contacts: DIY.PLC.314@gmail.com
// License: GNU GPL v2
