FUNCTION_BLOCK FbDeriveF //Производная с фильтром.
TITLE = 'Производная с фильтром.'

//      DbDeriveF
//    +-----------+
//    | FbDeriveF |
// ->-|In      Out|->-
//   -|Kdf        |
//   -|Ts         |
//    +-----------+

{//Атрибуты для PCS7.
S7_read_back := 'true' ; //CFC: Chart>Readback активен для прототипов DB.
S7_blockview := 'big'    //CFC: отображение блока маленькое или большое.
}

//Атрибуты для STEP7.
VERSION : '2.1'
AUTHOR  : VA
FAMILY  : LIB_PLC

VAR_INPUT //Входные переменные, сохраняемые.
In  :REAL := 0.0; //Входной сигнал.
Kdf :REAL := 1.0; //Коэффициент фильтрации дифференциальный Kdf=1/Tdf.
Ts  :REAL := 0.1; //Шаг дискретизации по времени [с].
END_VAR

VAR_OUTPUT //Выходные переменные, сохраняемые.
Out :REAL := 0.0; //Выходной сигнал.
END_VAR

VAR //Внутренние переменные, сохраняемые.
Dintegral :REAL := 0.0; //Интегратор.
END_VAR

//W(s)=s/(1+Tdf*s) при Tdf=1/Kdf и Ts->0.
IF (Kdf <= 0.0) THEN
Dintegral := 0.0;
Out := 0.0;
ELSE
Out := (In - Dintegral) * Kdf;
Dintegral := Dintegral + (Ts * Out);
END_IF;

END_FUNCTION_BLOCK

// Внутренняя схема фильтра.
//
//          Вычитание   Умножение
//            +---+     +------+
// In ----->--| + |     |      |
//            |   |-->--| *Kdf |-->----+-->-- Out
//      +-->--| - |     |      |       |
//      |     +---+     +------+       |
//      |                              |
//      |              Интегрирование  |
//      |              +----------+    |
//      |   Dintegral  |  Ts * z  |    |
//      +--------------| -------- |----+
//                     |   z - 1  |
//                     +----------+

// Условное графическое обозначение (приближенное).
//
//    +-----------------+
//    |        s        |
// ->-| --------------- |->-
//    | (1/Kdf) * s + 1 |
//    +-----------------+

// Электрическая схема фильтра.
//
// +    || C            +
// o----||----o---------o
//      ||    |          
//  U In      |     U Out
//           +-+         
//           | | R       
//           | |         
//           +-+         
//            |          
// -          |         -
// o----------o---------o
// Tf = 1/Kdf = R * C ([с] = [Ом] * [Ф])

// Временная характеристика.
//
// ^ Out
// |
// 1*            Y(t) = 0 - exp(-(t/Tf))
// | *          
// |  *        
// |    *       
// |      *     
// 0---------***---> t[s]
//
// ^ In
// |
// 1************ X(t) = 1
// |            
// |            
// |            
// |            
// 0---------------> t[s]

//  +---------+
//  | GNU GPL |
//  +---------+
//  |
//  |
//  .= .-_-. =.
// ((_/)o o(\_))
//  `-'(. .)`-'
//  |/| \_/ |\
//  ( |     | )
//  /"\_____/"\
//  \__)   (__/
// @COPYLEFT ALL WRONGS RESERVED :)
// Author: VA
// Contacts: DIY.PLC.314@gmail.com
// Date: 2014 - 2024
// License: GNU GPL-2.0-or-later
// https://www.gnu.org/licenses/old-licenses/gpl-2.0.html
// https://www.youtube.com/watch?v=n1F_MfLRlX0
//
// See also:
// https://www.youtube.com/@DIY_PLC
// https://github.com/DIYPLC/LIB_PLC
// https://oshwlab.com/diy.plc.314/PLC_HW1_SW1
// https://3dtoday.ru/3d-models/mechanical-parts/body/korpus-na-din-reiku
// https://t.me/DIY_PLC
