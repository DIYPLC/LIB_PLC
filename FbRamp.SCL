FUNCTION_BLOCK "FbRamp"
TITLE = 'Рампа разгона / торможения.'

{ S7_read_back := 'true' ;
  S7_blockview := 'big'  }

AUTHOR  : VA
FAMILY  : LIB_PLC
VERSION : '3.0'

VAR_INPUT // Входные переменные, сохраняемые.
In    :REAL := 0.0  ; // Входной сигнал.
Scale :REAL := 100.0; // Диапазон времени разгона и торможения.
TAcc  :REAL := 5.0  ; // Время разгона от 0 до Scale [с].
TDec  :REAL := 5.0  ; // Время торможения от Scale до 0 [c].
Ts    :REAL := 0.1  ; // Шаг дискретизации по времени [с].
END_VAR

VAR_OUTPUT // Выходные переменные, сохраняемые.
Out :REAL := 0.0; // Выходной сигнал.
END_VAR

VAR_TEMP // Временные переменные, не сохраняемые.
AccMode :BOOL; // Активен режим ускорения.
DecMode :BOOL; // Активен режим замедления.
END_VAR

BEGIN

// Рампа разгона / торможения.
//         DbRamp
//    +--------------+
//    |    FbRamp    |
// ->-|In         Out|->-
//   -|Scale         |
//   -|TAcc          |
//   -|TDec          |
//   -|Ts            |
//    +--------------+

// Режим байпаса интегратора рампы с безударным переходом.
IF (Scale <= 0.0) THEN
    Out := In;
ELSE // Штатный режим работы.

    // Состояние Out до отработки шага интегрирования.
    // Оптимизировать и переместить сравнения нельзя.
    AccMode := (In > Out); // Активен режим ускорения.
    DecMode := (In < Out); // Активен режим замедления.

    // Режим ускорения рампы.
    IF (AccMode) THEN
        IF (TAcc <= 0.0) THEN
        Out := In;
        ELSE
        Out := Out + ((Ts * Scale) / TAcc);
        END_IF;
    END_IF;

    // Режим замедления рампы.
    IF (DecMode) THEN
        IF (TDec <= 0.0) THEN
        Out := In;
        ELSE
        Out := Out - ((Ts * Scale) / TDec);
        END_IF;
    END_IF;

    // Установившийся режим рампы.
    // Состояние Out после отработки шага интегрирования.
    IF ( ((AccMode) AND (Out >= In))
      OR ((DecMode) AND (Out <= In))
      OR (In = Out) ) THEN
    Out := In;
    END_IF;

END_IF;

// Пример использования №1.
// Скорость открытия парового клапана от 0 до 100 процентов за 90  секунд.
// Скорость закрытия парового клапана от 100 до 0 процентов за 150 секунд.
// Параметры рампы для решения этой задачи:
// DbRamp1.Scale := 100.0; //Диапазон времени разгона и торможения.
// DbRamp1.TAcc  := 90.0 ; //Время разгона от 0 до Scale [с].
// DbRamp1.TDec  := 150.0; //Время торможения от Scale до 0 [c].

// Пример использования №2.
// Скорость разгона частотного привода от 0 до 50 Герц за 20 секунд.
// Скорость торможения частотного привода от 50 до 0 Герц за 15 секунд. 
// Параметры рампы для решения этой задачи:
// DbRamp2.Scale := 50.0; //Диапазон времени разгона и торможения.
// DbRamp2.TAcc  := 20.0; //Время разгона от 0 до Scale [с].
// DbRamp2.TDec  := 15.0; //Время торможения от Scale до 0 [c].

// Пример использования №3.
// Необходимо отключть рампу и передать сигнал напрямую с входа на выход. 
// Параметры рампы для решения этой задачи:
// DbRamp3.Scale := 0.0; //Диапазон времени разгона и торможения.

// Временная характеристика.
//
// ^ In
// |
// 100 *************
// |   *           *
// |   *           *
// |   *           *
// 0---------------------------------> t[s]
//
// ^ Out
// |
// |   TAcc         TDec
// |    10s          5s
// |  <----->       <-->
// 100       *******     ^
// |       *        *    | Scale 100%
// |     *           *   |
// |   *              *  v
// 0---------------------------------> t[s]
//
// Угол наклона рампы.
// tg(a) = Scale / TAcc
//

// Блок схема.
//
// Константы    Переключатель    Интегратор
// +-------+     +-----------+
// | Scale |     | In > Out  |
// | ----- |-->--|--+        |
// | TAcc  |     |   \       |
// +-------+     |    \      |
//               |     \     |
// +-------+     |      \    |     +-----+
// |       |     |       \   |     |  1  |
// |   0   |-->--|--+     +--|-->--| --- |-->-- Out
// |       |     | In ~= Out |     |  s  |
// +-------+     |           |     +-----+
//               |           |
// +-------+     |           |
// | Scale |     |           |
// | ----- |-->--|--+        |
// | TDec  |     | In < Out  |
// +-------+     +-----------+

// @COPYLEFT ALL WRONGS RESERVED :)
// Author: VA
// Contacts: DIY.PLC.314@gmail.com
// Date start LIB_PLC: 2014
// License: GNU GPL-2.0-or-later
// https://www.gnu.org/licenses/old-licenses/gpl-2.0.html
// https://www.youtube.com/watch?v=n1F_MfLRlX0
// https://www.youtube.com/@DIY_PLC
// https://github.com/DIYPLC/LIB_PLC
// https://oshwlab.com/diy.plc.314/PLC_HW1_SW1
// https://3dtoday.ru/3d-models/mechanical-parts/body/korpus-na-din-reiku
// https://t.me/DIY_PLC

END_FUNCTION_BLOCK
