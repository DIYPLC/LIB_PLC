FUNCTION_BLOCK "FbModeSelector"
TITLE = Переключатель режимов работы.
{ S7_Optimized_Access := 'FALSE' }
AUTHOR : VA
FAMILY : LIB_PLC
VERSION : 2.1
   VAR_INPUT 
      ButtonStop : Bool;   // Кнопка "Стоп".
      ButtonManual : Bool;   // Кнопка "Ручной режим".
      ButtonAuto : Bool;   // Кнопка "Автоматический режим".
      Error : Bool;   // Отсутствие готовности системы к работе.
      Reset : Bool;   // Сброс при перезагрузке.
   END_VAR

   VAR_OUTPUT 
      LampStop : Bool;   // Флаг "Активен режим СТОП".
      LampManual : Bool;   // Флаг "Активен РУЧНОЙ режим".
      LampAuto : Bool;   // Флаг "Активен АВТОМАТИЧЕСКИЙ режим.
   END_VAR

   VAR 
      State1 : Int;   // Локальный граф состояний.
      FlagError : Bool;   // Флаг запрещенного состояния входов.
   END_VAR

   VAR CONSTANT 
      STATE_STOP : Int;   // Состояние "СТОП РЕЖИМ".
      STATE_MANUAL : Int := 1;   // Состояние "РУЧНОЙ РЕЖИМ".
      STATE_AUTO : Int := 2;   // Состояние "АВТОМАТИЧЕСКИЙ РЕЖИМ".
   END_VAR


BEGIN
	
	
	// Переключатель режимов работы.
	//          DbModeSelector
	//    +------------------------+
	//    |     FbModeSelector     |
	// ->-|ButtonStop      LampStop|->-
	// ->-|ButtonManual  LampManual|->-
	// ->-|ButtonAuto      LampAuto|->-
	//   -|Error                   |
	//   -|Reset                   |
	//    +------------------------+
	
	// Запрещенное состояние:
	// Если вместе с кнопкой стоп одновременно нажали еще что-то.
	#FlagError:=
	((#ButtonStop AND #ButtonAuto) OR
	( #ButtonStop AND #ButtonManual));
	
	// При перезагрузке контроллера режим СТОП.
	IF (#Reset OR #FlagError) THEN
	    #State1     := #STATE_STOP;
	    #LampStop   := TRUE;
	    #LampManual := FALSE;
	    #LampAuto   := FALSE;
	ELSE
	    // Граф состояний.
	    CASE #State1 OF
	
	    #STATE_STOP: // Режим СТОП.
	    #LampStop   := TRUE;
	    #LampManual := FALSE;
	    #LampAuto   := FALSE;
	    // Переход СТОП -> РУЧНОЙ.
	    IF #ButtonManual THEN
	    #State1     := #STATE_MANUAL;
	    #LampStop   := FALSE;
	    #LampManual := TRUE;
	    #LampAuto   := FALSE;
	    END_IF;
	    // Переход СТОП -> АВТОМАТ.
	    IF (#ButtonAuto AND NOT(#Error)) THEN
	    #State1     := #STATE_AUTO;
	    #LampStop   := FALSE;
	    #LampManual := FALSE;
	    #LampAuto   := TRUE;
	    END_IF;
	
	    #STATE_MANUAL: // Режим РУЧНОЙ.
	    #LampStop   := FALSE;
	    #LampManual := TRUE;
	    #LampAuto   := FALSE;
	    // Переход РУЧНОЙ -> СТОП.
	    IF #ButtonStop THEN
	    #State1     := 0;
	    #LampStop   := TRUE;
	    #LampManual := FALSE;
	    #LampAuto   := FALSE;
	    END_IF;
	    // Преход РУЧНОЙ -> АВТОМАТ.
	    IF (#ButtonAuto AND NOT(#Error)) THEN
	    #State1     := #STATE_AUTO;
	    #LampStop   := FALSE;
	    #LampManual := FALSE;
	    #LampAuto   := TRUE;
	    END_IF;
	
	    #STATE_AUTO: // Режим АВТОМАТ.
	    #LampStop   := FALSE;
	    #LampManual := FALSE;
	    #LampAuto   := TRUE;
	    // Переход АВТОМАТ -> СТОП.
	    IF (#ButtonStop OR #Error) THEN
	    #State1     := #STATE_STOP;
	    #LampStop   := TRUE;
	    #LampManual := FALSE;
	    #LampAuto   := FALSE;
	    END_IF;
	    // Переход АВТОМАТ -> РУЧНОЙ.
	    IF #ButtonManual THEN
	    #State1     := #STATE_MANUAL;
	    #LampStop   := FALSE;
	    #LampManual := TRUE;
	    #LampAuto   := FALSE;
	    END_IF;
	
	    ELSE //СТОП при неопределенном состоянии.
	    #State1 := #STATE_STOP;
	    END_CASE;
	
	END_IF; // IF (Reset OR FlagError) THEN
	
	// Граф состояний.
	//
	// Если ошибка Error активна можно работать только в ручном режиме.
	// Reset должен срабатывать при перезагрузке или включении питания.
	//
	//                                  |
	//                                 -+-Reset OR FlagError
	//                                  |
	//                                  V
	//                        +-------------------+
	//                        | State1 =          |
	//            +---------->| STATE_STOP        |-----------+
	//            |           | LampStop = 1      |           |
	//            |           +-------------------+           |
	//            |                |         ^                |
	//            |                |         |                |
	//            |   ButtonManual-+-       -+-ButtonStop     |
	//            |                |         |                |
	//            |                V         |                |
	//            |           +-------------------+           |
	// ButtonStop |           | State1 =          |           | ButtonAuto
	//        OR -+-          | STATE_MANUAL      |          -+- AND
	//      Error |           | LampManual = 1    |           | NOT(Error)
	//            |           +-------------------+           |
	//            |                |         ^                |
	//            |     ButtonAuto |         |                |
	//            |           AND -+-       -+-ButtonManual   |
	//            |     NOT(Error) |         |                |
	//            |                V         |                |
	//            |           +-------------------+           |
	//            |           | State1 =          |           |
	//            +-----------| STATE_AUTO        |<----------+
	//                        | LampAuto = 1      |
	//                        +-------------------+
	
	// Пример организации интерфейса пользователя для HMI.
	// При нажатии кнопоки AUTO тег ButtonAuto = 1.
	// В автоматическом режиме работы тег LampAuto = 1 дает эффект нажатия на кнопку AUTO.
	// Кнопки MANUAL, STOP работают аналогично.
	// Так как кнопки без фиксации то можно параллельно кнопкам на экране подключать физические кнопки.
	//
	// +--------------------------------------------------------+
	// | MENU: "Errors"                                         |
	// +--------------------------------------------------------+
	// | 0. No Error (OK).                                      |
	// |                                                        |
	// |                                                        |
	// |                                                        |
	// |                                                        |
	// |                                                        |
	// |                                                        |
	// +--------------------------------------------------------+
	// | +--------+ +--------+ +--------+ +--------+ +--------+ |
	// | | < MENU | |  AUTO  | | MANUAL | |  STOP  | | MENU > | |
	// | +--------+ +--------+ +--------+ +--------+ +--------+ |
	// +--------------------------------------------------------+
	
	// @COPYLEFT ALL WRONGS RESERVED :)
	// Author: VA
	// Contacts: DIY.PLC.314@gmail.com
	// Date start LIB_PLC: 2014
	// License: GNU GPL-2.0-or-later
	// https://www.gnu.org/licenses/old-licenses/gpl-2.0.html
	// https://www.youtube.com/watch?v=n1F_MfLRlX0
	// https://www.youtube.com/@DIY_PLC
	// https://github.com/DIYPLC/LIB_PLC
	// https://oshwlab.com/diy.plc.314/PLC_HW1_SW1
	// https://3dtoday.ru/3d-models/mechanical-parts/body/korpus-na-din-reiku
	// https://t.me/DIY_PLC
	
	
END_FUNCTION_BLOCK

