FUNCTION_BLOCK "FbAlarm"
TITLE = Блок предупреждений.
{ S7_Optimized_Access := 'FALSE' }
AUTHOR : VA
FAMILY : LIB_PLC
VERSION : 2.2
   VAR_INPUT 
      In : Real;   // Входной сигнал.
      LevelHi : Real := 9.0e+1;   // Верхняя граница предупреждения.
      LevelLo : Real := 1.0e+1;   // Нижняя граница предупреждения.
      TimeDelay : DInt := L#2000;   // Время задержки включения [мс].
      Ts_ms : DInt := L#100;   // Шаг дискретизации по времени [мс].
   END_VAR

   VAR_OUTPUT 
      AlarmHi : Bool;   // Достигнут верхний предел.
      AlarmLo : Bool;   // Достигнут нижний предел.
   END_VAR

   VAR 
      _AlarmHi : Bool;   // Достигнут верхний предел.
      _AlarmLo : Bool;   // Достигнут нижний предел.
      Timer1 : DInt;   // Текущее время таймер1 [мс].
      Timer2 : DInt;   // Текущее время таймер1 [мс].
   END_VAR


BEGIN
	
	
	// Блок предупреждений.
	//          DbAlarm
	//    +-----------------+
	//    |     FbAlarm     |
	// ->-|In        AlarmHi|->-
	//   -|LevelHi   AlarmLo|->-
	//   -|LevelLo          |
	//   -|TimeDelay        |
	//   -|Ts_ms            |
	//    +-----------------+
	
	// Верхний предел.
	#_AlarmHi := (#In >= #LevelHi);
	
	// Таймер задержки включения для верхнего предела.
	IF (#TimeDelay > 0) THEN // Таймер включен.
	  IF (#_AlarmHi) THEN // На входе 1.
	    IF (#AlarmHi) THEN // На выходе 1.
	    #AlarmHi := TRUE;
	    ELSE // На выходе 0.
	      IF (#Timer1 >= #TimeDelay) THEN // Задержка включения закончилась.
	      #AlarmHi := TRUE;
	      ELSE // Задержка включения активна.
	      #AlarmHi := FALSE;
	      #Timer1 := #Timer1 + #Ts_ms;
	      END_IF;
	    END_IF;
	  ELSE // Если на входе 0 то на выходе тоже 0.
	  #AlarmHi := FALSE;
	  #Timer1 := 0;
	  END_IF;
	ELSE // Таймер выключен.
	#AlarmHi := #_AlarmHi;
	#Timer1 := 0;
	END_IF;
	
	// Нижний предел.
	#_AlarmLo := (#In <= #LevelLo);
	
	// Таймер задержки включения для нижнего предела.
	IF (#TimeDelay > 0) THEN // Таймер включен.
	  IF (#_AlarmLo) THEN // На входе 1.
	    IF (#AlarmLo) THEN // На выходе 1.
	    #AlarmLo := TRUE;
	    ELSE // На выходе 0.
	      IF (#Timer2 >= #TimeDelay) THEN // Задержка включения закончилась.
	      #AlarmLo := TRUE;
	      ELSE // Задержка включения активна.
	      #AlarmLo := FALSE;
	      #Timer2 := #Timer2 + #Ts_ms;
	      END_IF;
	    END_IF;
	  ELSE // Если на входе 0 то на выходе тоже 0.
	  #AlarmLo := FALSE;
	  #Timer2 := 0;
	  END_IF;
	ELSE // Таймер выключен.
	#AlarmLo := #_AlarmLo;
	#Timer2 := 0;
	END_IF;
	
	// @COPYLEFT ALL WRONGS RESERVED :)
	// Author: VA
	// Contacts: DIY.PLC.314@gmail.com
	// Date start LIB_PLC: 2014
	// License: GNU GPL-2.0-or-later
	// https://www.gnu.org/licenses/old-licenses/gpl-2.0.html
	// https://www.youtube.com/watch?v=n1F_MfLRlX0
	// https://www.youtube.com/@DIY_PLC
	// https://github.com/DIYPLC/LIB_PLC
	// https://oshwlab.com/diy.plc.314/PLC_HW1_SW1
	// https://3dtoday.ru/3d-models/mechanical-parts/body/korpus-na-din-reiku
	// https://t.me/DIY_PLC
	
	
END_FUNCTION_BLOCK

