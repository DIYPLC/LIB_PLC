FUNCTION_BLOCK "Fb2PosControl"
TITLE = Регулятор двухпозиционный.
{ S7_Optimized_Access := 'FALSE' }
AUTHOR : VA
FAMILY : LIB_PLC
VERSION : 2.1
   VAR_INPUT 
      PV : Real;   // Измеренное значение регулируемого параметра.
      SP : Real;   // Заданное значение регулируемого параметра.
      HYST : Real := 5.0e+0;   // Гистерезис > 0.
   END_VAR

   VAR_OUTPUT 
      OP : Bool;   // Выход управляющего сигнала.
   END_VAR

   VAR 
      ER : Real;   // Ошибка регулирования.
   END_VAR


BEGIN
	
	
	// Регулятор двухпозиционный.
	//       Db2PosControl
	//     +---------------+
	//     | Fb2PosControl |
	//  ->-|PV           OP|->-
	//    -|SP             |
	//    -|HYST           |
	//     +---------------+
	
	// Ошибка регулирования.
	#ER := #SP - #PV;
	
	// Регулятор двухпозиционный.
	IF (#ER >= #HYST) THEN
	#OP:=TRUE;
	END_IF;
	
	IF (#ER <= #HYST * (-1.0)) THEN
	#OP := FALSE;
	END_IF;
	
	// Применение.
	//
	// Двухпозиционный регулятор используется как простейший регулятор температуры, давления, уровня.
	
	// Условное графическое обозначение.
	//
	//      +---------------+
	//      |               |
	//      |    +--<--+--  |
	//      |    |     |    |
	// ER->-|    v     ^    |->-OP
	//      |    |     |    |
	//      |  --+-->--+    |
	//      |               |
	//      +---------------+
	
	// Статическая характеристика.
	//
	//                 OP
	//                  ^
	//                  |
	//           ***<***1***<******
	//           *      |      *
	//           v      |      ^
	//           *      |      *
	// -ER <--******>***0***>***--> +ER
	//           |      |      |
	//           |<---->|<---->|
	//             HYST   HYST
	
	// Временная характеристика.
	//
	// ^ PV,SP
	// |         PV
	// |----------*---------------*------
	// |         * *             * *   |
	// |        *   *           *   *  |  SP + HYST
	// |       *     *         *       |
	// |======*=======*=======*========== SP
	// |     *         *     *         |
	// |    *           *   *          |  SP - HYST
	// |   *             * *           |
	// |--*---------------*--------------
	// | * 
	// |*
	// +-----------------------------------------> t[s]
	//
	// ^ OP
	// |
	// 1***********       *********
	// |          *       *       *
	// 0----------*********-------***------------> t[s]
	
	// Схема включения.
	//
	//                  +---------------+
	//                  |               |   Объект управления
	//                  |    +--<--+--  |     +-----------+
	//        +---+ ER  |    |     |    | OP  |     K     |
	// SP-->--| + |-->--|    v     ^    |-->--| --------- |-->--+-->PV
	// PV  +--| - |     |    |     |    |     | T * s + 1 |     |
	//     |  +---+     |  --+-->--+    |     +-----------+     |
	//     |            |               |                       |
	//     |            +---------------+                       |
	//     |                                                    |
	//     +----------------------------------------------------+
	
	// @COPYLEFT ALL WRONGS RESERVED :)
	// Author: VA
	// Contacts: DIY.PLC.314@gmail.com
	// Date start LIB_PLC: 2014
	// License: GNU GPL-2.0-or-later
	// https://www.gnu.org/licenses/old-licenses/gpl-2.0.html
	// https://www.youtube.com/watch?v=n1F_MfLRlX0
	// https://www.youtube.com/@DIY_PLC
	// https://github.com/DIYPLC/LIB_PLC
	// https://oshwlab.com/diy.plc.314/PLC_HW1_SW1
	// https://3dtoday.ru/3d-models/mechanical-parts/body/korpus-na-din-reiku
	// https://t.me/DIY_PLC
	
	
END_FUNCTION_BLOCK

