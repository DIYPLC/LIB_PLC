FUNCTION_BLOCK "Fb3PosControl"
TITLE = Регулятор трехпозиционный.
{ S7_Optimized_Access := 'FALSE' }
AUTHOR : VA
FAMILY : LIB_PLC
VERSION : 2.2
   VAR_INPUT 
      PV : Real;   // Измеренное значение регулируемого параметра.
      SP : Real;   // Заданное значение регулируемого параметра.
      DBand : Real := 2.0e+0;   // Зона нечувствительности > 0.
      HystPos : Real := 1.0e+0;   // Гистерезис > 0.
      HystNeg : Real := 1.0e+0;   // Гистерезис > 0.
   END_VAR

   VAR_OUTPUT 
      OpPos : Bool;   // Выход управляющего сигнала положительный.
      OpNeg : Bool;   // Выход управляющего сигнала отрицательный.
   END_VAR

   VAR 
      ER : Real;   // Ошибка регулирования.
   END_VAR


BEGIN
	
	
	// Регулятор трехпозиционный.
	//       Db3PosControl
	//     +---------------+
	//     | Fb3PosControl |
	//  ->-|PV        OpPos|->-
	//    -|SP        OpNeg|->-
	//    -|DBand          |
	//    -|HystPos        |
	//    -|HystNeg        |
	//     +---------------+
	
	// Ошибка регулирования.
	#ER := #SP - #PV;
	
	// ВКЛ нагрев или движение вперед.
	IF (#ER >= (#DBand + #HystPos))
	THEN
	#OpPos := TRUE;
	END_IF;
	
	// ВЫКЛ нагрев или движение вперед.
	IF (#ER <= (#DBand - #HystPos))
	THEN
	#OpPos := FALSE;
	END_IF;
	
	// ВКЛ охлаждение или движение назад.
	IF (#ER <= (0.0 - #DBand - #HystNeg))
	THEN
	#OpNeg := TRUE;
	END_IF;
	
	// ВЫКЛ охлаждение или движение назад.
	IF (#ER >= (0.0 - #DBand + #HystNeg))
	THEN
	#OpNeg := FALSE;
	END_IF;
	
	// Применение.
	//
	// Трехпозиционный регулятор используется как простейший регулятор температуры или положения.
	
	// Статическая характеристика.
	//
	//                           OpPos
	//                             ^
	//                             |  DBand
	//                             |<-------->|
	//                             |          |
	//                             |   HystPos|HystPos
	//                             |   |<---->|<---->|
	//                             |   |      |      |
	//                            1|---***<***1***<******
	//                             |   *      |      *
	//                             |   v      |      ^
	//                             |   *      |      *
	// -ER <-----***<*******<******0******>*******>***-----> +ER
	//           *      |      *   |
	//           v      |      ^   |
	//           *      |      *   |
	//        ******>***1***>***---|-1
	//           |      |      |   |
	//           |<---->|<---->|   |
	//           HystHeg|HystHeg   |
	//                  |          |
	//                  |<-------->|
	//                     DBand   |
	//                             v
	//                           OpNeg
	
	// Схема включения.
	//
	//        +---------------+     +-------------+     +-------------+
	// SP-->--|SP        OpPos|-->--| Нагреватель |-->--| Датчик      |--+-->PV
	//        |               |     +-------------+     | температуры |  |
	//        |               |                         |             |  |
	//        |               |     +-------------+     |             |  |
	//     +--|PV        OpNeg|-->--| Охладитель  |-->--|             |  |
	//     |  +---------------+     +-------------+     +-------------+  |
	//     |                                                             |
	//     +-------------------------------------------------------------+
	//
	//        +---------------+     +--------------+     +-------------+
	// SP-->--|SP        OpPos|-->--| Мотор вперед |-->--| Датчик      |--+-->PV
	//        |               |     +--------------+     | положения   |  |
	//        |               |                          |             |  |
	//        |               |     +--------------+     |             |  |
	//     +--|PV        OpNeg|-->--| Мотор назад  |-->--|             |  |
	//     |  +---------------+     +--------------+     +-------------+  |
	//     |                                                              |
	//     +--------------------------------------------------------------+
	
	// @COPYLEFT ALL WRONGS RESERVED :)
	// Author: VA
	// Contacts: DIY.PLC.314@gmail.com
	// Date start LIB_PLC: 2014
	// License: GNU GPL-2.0-or-later
	// https://www.gnu.org/licenses/old-licenses/gpl-2.0.html
	// https://www.youtube.com/watch?v=n1F_MfLRlX0
	// https://www.youtube.com/@DIY_PLC
	// https://github.com/DIYPLC/LIB_PLC
	// https://oshwlab.com/diy.plc.314/PLC_HW1_SW1
	// https://3dtoday.ru/3d-models/mechanical-parts/body/korpus-na-din-reiku
	// https://t.me/DIY_PLC
	
	
END_FUNCTION_BLOCK

