FUNCTION_BLOCK "FbDriverAo420"
TITLE = 'Драйвер аналогово выхода 4...20мА.'

{ S7_read_back := 'true' ;
  S7_blockview := 'big'  }

AUTHOR  : VA
FAMILY  : LIB_PLC
VERSION : '2.2'

//Драйвер аналогово выхода 4...20мА.
VAR_INPUT //Входные переменные, сохраняемые.
In       :REAL := 0.0  ; //Вход сигнала на исполнительный механизм.
InMax    :REAL := 100.0; //Исполнительный механизм максимум.
InMin    :REAL := 0.0  ; //Исполнительный механизм минимум.
SimValue :REAL := 0.0  ; //Значение для исполнительного механизма при симуляции.
SimOn    :BOOL := FALSE; //Включить симуляцию.
END_VAR

VAR_OUTPUT //Выходные переменные, сохраняемые.
DAC     :WORD := 0  ; //ЦАП Сигнал на исполнительный механизм 0...27648.
Current :REAL := 0.0; //Ток на аналоговом выходе 4...20мА.
END_VAR

VAR
InLim   :REAL := 0.0; //Сигнал исполнительного механизма после ограничения.
Switch1 :REAL := 0.0; //Переключатель.
END_VAR

BEGIN

//          DbDriverAo420
//    +-----------------------+
//    |     FbDriverAo420     |
// ->-|In                  DAC|->-
//   -|InMax           Current|-
//   -|InMin                  |
//   -|SimValue               |
//   -|SimOn                  |
//    +-----------------------+

//Амплитудный ограничитель диапазона исполнительного механизма InMin...InMax
IF (In >= InMax) THEN //Ограничение выхода сверху.
InLim := InMax; 
ELSE
    IF (In <= InMin) THEN //Ограничение выхода снизу.
    InLim := InMin;
    ELSE
    //Выход без ограничений.
    InLim := In;
    END_IF;
END_IF;

IF (SimOn) THEN //Включена симуляция.
Switch1 := SimValue;
ELSE
Switch1 := InLim;
END_IF;

//ЦАП Сигнал на исполнительный механизм 0...27648.
//Масштабирование InMin...InMax -> 0...27648
DAC := INT_TO_WORD( REAL_TO_INT( ((Switch1 - InMin) / (InMax - InMin)) * 27648.0) );

//Ток на аналоговом выходе 4...20мА.
IF (InMax = InMin) THEN //Защита от деления на 0.
Current := 0.0;
ELSE
//Масштабирование InMin...InMax -> 4...20[мА]
Current := (((InLim - InMin) / (InMax - InMin)) * 16.0) + 4.0;
END_IF;

//Описание работы драйвера аналогово входа 4...20мА.
//1.Сигнал исполнительного механизма ограничивается в пределах его диаппазона измерения InMin...InMax.
//2.Если не включена симуляция сигнал c входа In преобразуется в сигнал ЦАП.
//3.Если включена симуляция сигнал c входа SimValue преобразуется в сигнал ЦАП.
//4.Рассчитывается значение тока 4...20мА для индикации.

// @COPYLEFT ALL WRONGS RESERVED :)
// Author: VA
// Contacts: DIY.PLC.314@gmail.com
// Date start LIB_PLC: 2014
// License: GNU GPL-2.0-or-later
// https://www.gnu.org/licenses/old-licenses/gpl-2.0.html
// https://www.youtube.com/watch?v=n1F_MfLRlX0
// https://www.youtube.com/@DIY_PLC
// https://github.com/DIYPLC/LIB_PLC
// https://oshwlab.com/diy.plc.314/PLC_HW1_SW1
// https://3dtoday.ru/3d-models/mechanical-parts/body/korpus-na-din-reiku
// https://t.me/DIY_PLC

END_FUNCTION_BLOCK
