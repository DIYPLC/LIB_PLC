FUNCTION_BLOCK FbPIDsteam //ПИД-регулятор давления пара быстромедленный.
TITLE = 'ПИД-регулятор давления пара быстромедленный.'

//          DbPIDsteam
//    +--------------------+
//    |     FbPIDsteam     |
// ->-|Pressure       Valve|->-
//   -|Setpoint            |
//   -|Kp                  |
//   -|Ki                  |
//   -|Kd                  |
//   -|Kdf                 |
//   -|ERMAX               |
//   -|ERMIN               |
//   -|OutMax              |
//   -|OutMin              |
//   -|Ts                  |
//   -|Manual              |
//   -|OnMan               |
//   -|OutLocalLimitMax    |
//   -|OutLocalLimitMin    |
//   -|OutRampTAcc         |
//   -|OutRampTDec         |
//    +--------------------+

{//Атрибуты для PCS7.
S7_read_back := 'true' ; //CFC: Chart>Readback активен для прототипов DB.
S7_blockview := 'big'    //CFC: отображение блока маленькое или большое.
}

//Атрибуты для STEP7.
VERSION : '1.2'
AUTHOR  : VA
FAMILY  : LIB_PLC

VAR_INPUT //Входные переменные, сохраняемые.
Pressure :REAL := 0.0   ; //Измеренное давление пара [Bar].
Setpoint :REAL := 2.0   ; //Заданное давление пара [Bar].
Kp       :REAL := 0.001 ; //Коэффициент усиления пропорциональный.
Ki       :REAL := 0.001 ; //Коэффициент усиления интегральный.
Kd       :REAL := 0.0   ; //Коэффициент усиления дифференциальный.
Kdf      :REAL := 1.0   ; //Коэффициент фильтрации дифференциальный Kdf=1/Tdf.
ERMAX    :REAL :=  0.001; //Зона нечувствительности к ошибке регулирования, максимум.
ERMIN    :REAL := -0.001; //Зона нечувствительности к ошибке регулирования, минимум.
OutMax   :REAL := 100.0 ; //Глобальное ограничение амплитуды максимум [%].
OutMin   :REAL := 0.0   ; //Глобальное ограничение амплитуды минимум [%].
Ts       :REAL := 0.1   ; //Шаг дискретизации по времени в секундах.
Manual   :REAL := 25.0  ; //Сигнал управления в ручном режиме работы.
OnMan    :BOOL := FALSE ; //Включить ручной режим работы регулятора.
OutLocalLimitMax :REAL := 7.0  ; //Локальное ограничение амплитуды максимум [%].
OutLocalLimitMin :REAL := -7.0 ; //Локальное ограничение амплитуды максимум [%].
OutRampTAcc      :REAL := 150.0; //Время разгона для максимума локального ограничения амплитуды [c].
OutRampTDec      :REAL := 200.0; //Время торможения для минимума локального ограничения амплитуды [c].
END_VAR

VAR_OUTPUT //Выходные переменные, сохраняемые.
Valve :REAL := 0.0; //Сигнал управления паровым клапаном 0...100[%].
END_VAR

VAR //Внутренние переменные, сохраняемые.
DbPIDcontrol       :FbPIDcontrol; //ПИД-регулятор.
DbRamp_speed_limit :FbRamp      ; //Рампа разгона / торможения.
DbLimit_max        :FbLimit     ; //Амплитудный ограничитель.
DbLimit_min        :FbLimit     ; //Амплитудный ограничитель.
END_VAR

//Берем обычный быстро настроенный ПИД- регулятор и делаем для него медленно плавающее окно ограничения амплитуды.
//Выход регулятора в автоматическом режиме отправляем напрямую на управление паровым клапаном.
//ПИД-регулятор.
//         DbPIDcontrol
//    +--------------------+
//    |    FbPIDcontrol    |
// ->-|PV                MV|->-
//   -|SP                  |
//   -|Kp                  |
//   -|Ki                  |
//   -|Kd                  |
//   -|Kdf                 |
//   -|ERMAX               |
//   -|ERMIN               |
//   -|MVMAX               |
//   -|MVMIN               |
//   -|Ts                  |
//   -|Manual              |
//   -|OnMan               |
//    +--------------------+
DbPIDcontrol.PV      := Pressure       ; //Измеренное значение регулируемого параметра.
DbPIDcontrol.SP      := Setpoint       ; //Заданное значение регулируемого параметра.
DbPIDcontrol.Kp      := Kp             ; //Коэффициент усиления пропорциональный.
DbPIDcontrol.Ki      := Ki             ; //Коэффициент усиления интегральный.
DbPIDcontrol.Kd      := Kd             ; //Коэффициент усиления дифференциальный.
DbPIDcontrol.Kdf     := Kdf            ; //Коэффициент фильтрации дифференциальный Kdf=1/Tdf.
DbPIDcontrol.ERMAX   := ERMAX          ; //Зона нечувствительности к ошибке регулирования, максимум.
DbPIDcontrol.ERMIN   := ERMIN          ; //Зона нечувствительности к ошибке регулирования, минимум.
//DbPIDcontrol.MVMAX := 100.0          ; //Максимальное значение сигнала управления.
//DbPIDcontrol.MVMIN := 0.0            ; //Минимальное значение сигнала управления.
DbPIDcontrol.Ts      := Ts             ; //Шаг дискретизации по времени в секундах.
DbPIDcontrol.Manual  := Manual         ; //Сигнал управления в ручном режиме работы.
DbPIDcontrol.OnMan   := OnMan          ; //Включить ручной режим работы регулятора.
DbPIDcontrol()                         ; //ПИД-регулятор.
Valve                := DbPIDcontrol.MV; //Сигнал управления на исполнительный механизм.

//Ограничиваем скорость изменения сигнала управления с ПИД- регулятора.
//Ограниченную скорость передаем на амплитудные ограничители формирующие плавающий рабочий коридор.
//Рампа разгона / торможения.
//   DbRamp_speed_limit
//    +--------------+
//    |    FbRamp    |
// ->-|In         Out|->-
//   -|Scale         |
//   -|TAcc          |
//   -|TDec          |
//   -|Ts            |
//    +--------------+
DbRamp_speed_limit.In    := DbPIDcontrol.MV       ; //Входной сигнал.
DbRamp_speed_limit.Scale := 100.0                 ; //Диапазон времени разгона и торможения.
DbRamp_speed_limit.TAcc  := OutRampTAcc           ; //Время разгона от 0 до Scale [с].
DbRamp_speed_limit.TDec  := OutRampTDec           ; //Время торможения от Scale до 0 [c].
DbRamp_speed_limit.Ts    := Ts                    ; //Шаг дискретизации по времени [с].
DbRamp_speed_limit()                              ; //Рампа разгона / торможения.
//                       := DbRamp_speed_limit.Out; //Выходной сигнал.

//Верхняя граница плавающего окна.
//Амплитудный ограничитель.
//       DbLimit_max
//    +-----------+
//    |  FbLimit  |
// ->-|In      Out|->-
//   -|OutMax     |
//   -|OutMin     |
//    +-----------+
DbLimit_max.In     := DbRamp_speed_limit.Out + OutLocalLimitMax; //Вход амплитудного ограничителя.
DbLimit_max.OutMax := OutMax                                   ; //Максимальное значение выхода.
DbLimit_max.OutMin := OutMin                                   ; //Минимальное значение выхода.
DbLimit_max()                                                  ; //Амплитудный ограничитель.
//                 := DbLimit_max.Out                          ; //Выход амплитудного ограничителя.

//Нижняя граница плавающего окна.
//Амплитудный ограничитель.
//       DbLimit_min
//    +-----------+
//    |  FbLimit  |
// ->-|In      Out|->-
//   -|OutMax     |
//   -|OutMin     |
//    +-----------+
DbLimit_min.In     := DbRamp_speed_limit.Out + OutLocalLimitMin; //Вход амплитудного ограничителя.
DbLimit_min.OutMax := OutMax                                   ; //Максимальное значение выхода.
DbLimit_min.OutMin := OutMin                                   ; //Минимальное значение выхода.
DbLimit_min()                                                  ; //Амплитудный ограничитель.
//                 := DbLimit_min.Out                          ; //Выход амплитудного ограничителя.

//Порядок пересылки переменных важен на первом запуске программы.
//При первом запуске программы корректно отработает ограничение регулятора по умолчанию 0...100[%].
DbPIDcontrol.MVMAX := DbLimit_max.Out ; //Максимальное значение сигнала управления.
DbPIDcontrol.MVMIN := DbLimit_min.Out   ; //Минимальное значение сигнала управления.

END_FUNCTION_BLOCK

// Постановка задачи:
// Система стабилизации давления пара должна быстро реагировать на изменение давления по каналу управления и возмущения.
// С другой стороны паровой клапан нельзя закрывать слишком быстро иначе избыток пара может взорвать котел.
// То есть на лицо два противоречивых требования.
// Если ПИД- регулятор сделать медленным это будет безопасно для котла но невыгодно из за длительных переходных процессов.
// Если ПИД- регулятор сделать быстрым это будет выгодно но не безопасно для котла.
// Решение:
// Делаем ПИД-регулятор одновременно быстрым и медленным.
// Настраиваем ПИД-регулятор на быстрый переходной процесс.
// Но ограничиваем этот быстрый переходной процесс локальным амплитудным ограничителем -7%...+7%.
// При небольшом ходе клапана -7%...+7% паровой клапан перемещается быстро.
// Если клапану нужно отклониться более чем на -7%...+7% то это будет происходить медленно.
// При медленном движении клапана ПИД-регулятор уходит в амплитудное ограничение, а скорость открытия / закрытия формируется медленной рампой.
// То есть мы имеем маленькое окно локального ограничения которое передвигается в пределах большого статичного окна глобального ограничения.
// В пределам маленького окна -7%...+7% работаем быстро, а за его пределами медленно.
// Локальное окно амплитудного ограничителя медленно ползет за выходом с ПИД-регулятора.

//             DbPIDsteam
//       +--------------------+
//       |     FbPIDsteam     |
//    ->-|Pressure       Valve|->-
//      -|Setpoint            |
//      -|Kp                  |
//      -|Ki                  |
//      -|Kd                  |
//      -|Kdf                 |
//      -|ERMAX               |
//      -|ERMIN               |
//  100%-|OutMax              |
//    0%-|OutMin              |
//      -|Ts                  |
//      -|Manual              |
//      -|ManOn               |
//       |                    |
//   +5%-|OutLocalLimitMax    |
//   -5%-|OutLocalLimitMin    |
//  150s-|OutRampTAcc         |
//  200s-|OutRampTDec         |
//       |                    |
//       +--------------------+

//Структура регулятора:
//
//                         DbPIDcontrol
//   0...5[Bar]       +--------------------+        0...100[%]
//  +----------+      |    FbPIDcontrol    |        +-------+
//  | Pressure |---->-|ProcessVariable  Out|->-+----| Valve |
//  +----------+     -|Setpoint            |   |    +-------+                             DbLimit_max
//                   -|Kp                  |   |                                         +-----------+
//                   -|Ki                  |   |                               +-+       |  FbLimit  |
//                   -|Kd                  |   |                         +7%->-|+|----->-|In      Out|->---+
//                   -|Kdf                 |   |                           +->-|+|  100%-|OutMax     |     |
//                   -|ERMAX               |   |                           |   +-+    0%-|OutMin     |     |
//                   -|ERMIN               |   |     DbRamp_speed_limit    |             +-----------+     |
//           +------>-|OutMax              |   |      +------------+       |                               |
//           | +---->-|OutMin              |   |      |   FbRamp   |       |                               |
//           | |     -|Ts                  |   +---->-|In       Out|->-----+              DbLimit_min      |
//           | |     -|Manual              |   100%->-|Scale       |       |             +-----------+     |
//           | |     -|ManOn               |   150s->-|TAcc        |       |   +-+       |  FbLimit  |     |
//           | |      +--------------------+   200s->-|TDec        |       +->-|+|----->-|In      Out|->-+ |
//           | |                                     -|Ts          |     -7%->-|+|  100%-|OutMax     |   | |
//           | |                                      +------------+           +-+    0%-|OutMin     |   | |
//           | |                                                                         +-----------+   | |
//           | |                                                                                         | |
//           | +-----------------------------------------------------------------------------------------+ |
//           |                                                                                             |
//           +---------------------------------------------------------------------------------------------+
//
// 14% коридор работы клапана без ограничения скорости.
// OutLocalLimitMax = 7%
// OutLocalLimitMin = -7%
// Время открытия клапана 150 секунд.
// Окрывать клапан можно быстрее чем закрывать.
// OutRampTAcc = 150s
// Закрывать клапан надо медленно.
// Время закрытия клапана 200 секунд.
// OutRampTDec = 200s

// Времянная диаграмма работы.
//
//        ^ Valve%
//        |
//   100% |===========================================================================
//        |                              ^                                            
//        |              *               |                                            
//        |            *   *             +- Глобальное ограничение амплитуды максимум
//        |          *       *                                                        
//        |        *     *     *                                                      
//        |      *     *   *     *                                                    
//        |    *     *       *     *                                                  
//        |  *     *     *     *     *                                                
// PID+7% |*     *     *   *     *     * <- Локальное ограничение амплитуды максимум 
//        |    *     *       *     *                                                  
//        |  *     *           *     *                                                
//    PID |*     *               *     * <- Сигнал управления Valve                   
//        |    *                   *                                                  
//        |  *                       *                                                
// PID-7% |*                           * <- Локальное ограничение амплитуды минимум  
//        |                                                                           
//        |                              +- Глобальное ограничение амплитуды минимум 
//        |                              |                                            
//        |                              v                                            
//     0% +----------------------------------------------------------------------------> t[s]

// @COPYLEFT ALL WRONGS RESERVED :)
// Author: VA
// Contacts: DIY.PLC.314@gmail.com
// Date start LIB_PLC: 2014
// License: GNU GPL-2.0-or-later
// https://www.gnu.org/licenses/old-licenses/gpl-2.0.html
// https://www.youtube.com/watch?v=n1F_MfLRlX0
// https://www.youtube.com/@DIY_PLC
// https://github.com/DIYPLC/LIB_PLC
// https://oshwlab.com/diy.plc.314/PLC_HW1_SW1
// https://3dtoday.ru/3d-models/mechanical-parts/body/korpus-na-din-reiku
// https://t.me/DIY_PLC
