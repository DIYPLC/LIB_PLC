FUNCTION_BLOCK FbPIDcontrol //ПИД-регулятор.

//         DbPIDcontrol
//    +--------------------+
//    |    FbPIDcontrol    |
// ->-|ProcessVariable  Out|->-
//   -|Setpoint            |
//   -|Kp                  |
//   -|Ki                  |
//   -|Kd                  |
//   -|Kdf                 |
//   -|DBmax               |
//   -|DBmin               |
//   -|OutMax              |
//   -|OutMin              |
//   -|Ts                  |
//   -|Manual              |
//   -|ManOn               |
//    +--------------------+

//Атрибуты для STEP7.
TITLE   = 'ПИД-регулятор.'
VERSION : '3.0'
AUTHOR  : 'VA'
FAMILY  : 'LibPlc'
{//Атрибуты для PCS7.
S7_read_back := 'true' ; //CFC: Chart>Readback активен для прототипов DB.
S7_blockview := 'big'  ; //CFC: отображение блока мелнькое или большое.
//Опции компилятора SCL.
GenerateReferenceData := 'y' ; //SCL: Генерировать перекрестные ссылки.
CreateDebugInfo       := 'y' ; //SCL: Генерировать оладочную информацию.
SetOKFlag             := 'y' ; //SCL: ENO = 1
MonitorArrayLimits    := 'y' ; //SCL: Следить за границами массивов.
OptimizeObjectCode    := 'y'   //SCL: Оптимизация объектного кода.
}

VAR_INPUT //Входные переменные, сохраняемые.
ProcessVariable :REAL := 0.0   ; //Измеренное значение регулируемого параметра.
Setpoint        :REAL := 0.0   ; //Заданное значение регулируемого параметра.
Kp              :REAL := 0.001 ; //Коэффициент усиления пропорциональный.
Ki              :REAL := 0.001 ; //Коэффициент усиления интегральный.
Kd              :REAL := 0.0   ; //Коэффициент усиления дифференциальный.
Kdf             :REAL := 1.0   ; //Коэффициент фильтрации дифференциальный Kdf=1/Tdf.
DBmax           :REAL :=  0.001; //Зона нечувствительности к ошибке регулирования, максимум.
DBmin           :REAL := -0.001; //Зона нечувствительности к ошибке регулирования, минимум.
OutMax          :REAL := 100.0 ; //Максимальное значение сигнала управления.
OutMin          :REAL := 0.0   ; //Минимальное значение сигнала управления.
Ts              :REAL := 0.1   ; //Шаг дискретизации по времени в секундах.
Manual          :REAL := 25.0  ; //Сигнал управления в ручном режиме работы.
ManOn           :BOOL := FALSE ; //Включить ручной режим работы регулятора.
END_VAR

VAR_OUTPUT //Выходные переменные, сохраняемые.
Out :REAL := 0.0; //Сигнал управления на исполнительный механизм.
END_VAR

VAR //Внутренние переменные, сохраняемые.
Er        :REAL := 0.0; //Ошибка регулирования.
Ppart     :REAL := 0.0; //Пропорциональная составляющая регулятора.
Ipart     :REAL := 0.0; //Интегратор интегральной составляющей регулятора.
Dpart     :REAL := 0.0; //Дифференциальная составляющая регулятора.
Dintegral :REAL := 0.0; //Интегратор дифференциальной составляющей регулятора.
END_VAR

VAR_TEMP  //Временные переменные, не сохраняемые.
Auto :REAL; //Выход ПИД- алгоритма и вход переключателя режимов работы.
Sw   :REAL; //Выход с переключателя режимов работы.
END_VAR

//Ошибка регулирования.
Er := Setpoint - ProcessVariable;

//Зона нечувствительности к ошибке.
IF ((DBmin < Er) AND (Er < DBmax)) THEN
Er := 0.0;
END_IF;

//Пропорциональная составляющая.
Ppart := Kp * Er;

//Интегральная составляющая.
IF (Ki = 0.0) THEN
Ipart := 0.0;
ELSE
Ipart := Ipart + Ki * Er * Ts;
END_IF;

//Дифференциальная составляющая.
IF (Kd = 0.0) THEN
Dpart := 0.0;
Dintegral := 0.0;
ELSE
Dpart := (Er * Kd - Dintegral) * Kdf;
Dintegral := Dintegral + Dpart * Ts;
END_IF;

//Сигнал управления.
Auto := Ppart + Ipart + Dpart;

//Переключение режима работы "Ручной / Автоматический".
IF (ManOn) THEN
Sw := Manual;
ELSE
Sw := Auto;
END_IF;

//Амплитудный ограничитель- максимум.
IF (Sw >= OutMax) THEN
Out := OutMax;
//Ограничение интегральной составляющей методом обратного вычисления.
Ipart := Out - (Ppart + Dpart);
END_IF;

//Амплитудный ограничитель- минимум.
IF (Sw <= OutMin) THEN
Out := OutMin;
//Ограничение интегральной составляющей методом обратного вычисления.
Ipart := Out - (Ppart + Dpart);
END_IF;

//Амплитудный ограничитель- без ограничений.
IF ((OutMin < Sw) AND (Sw < OutMax)) THEN
Out := Sw;
  IF (ManOn) THEN
  //Ограничение интегральной составляющей методом обратного вычисления.
  Ipart := Out - (Ppart + Dpart);
  END_IF;
END_IF;

END_FUNCTION_BLOCK

// Передаточная функция регулятора (при Ts -> 0):
//
//         Out(s)               1              s
// W(s) = -------- = Kp + Ki * --- + Kd * -------------
//          Er(s)               s          Tdf * s + 1
//
// Er(s) = Setpoint(s) - ProcessVariable(s).
// Tdf = 1 / Kdf

// Передаточная функция интегратора:
//
//         Ts * z
// W(z) = --------
//         z - 1
//
// Численное интегрирование методом прямоугольников.
// y = y + x * Ts;

// Перечень сокращений.
//
// Fb          - Function block       - Функциональный блок.
// Db          - Data block           - Блок данных.
// PIDcontrol  - PID Controller       - ПИД- регулятор (пропорционально интегрально дифференциальный регулятор).
// SP          - Setpoint             - Заданное значение.
// PV          - Process Variable     - Измеренное значение.
// OP          - Output PIDcontroller - Сигнал управления.
// Er          - Error                - Ошибка регулирования, рассогласование.
// DBmax       - Dead band maximum    - Максимум зоны нечувствительности.
// DBmin       - Dead band minimum    - Минимум зоны нечувствительности.
// OPmax       - Output power maximum - Максимум сигнала управления.
// OPmin       - Output power minimum - Минимум сигнала управления.
// Ts          - Sample Time          - Шаг дискретизации по времени.
// ManOn       - Manual on            - Включить ручной режим управления.
// Sw          - Switch               - Переключатель.

// Характеристики ПИД- регулятора.
//
// Ограничение интегральной составляющей методом обратного вычисления.
// Зона нечувствительности к ошибке регулирования для устранения небольших колебаний регулятора в установившемся режиме работы.
// Амплитудное ограничение выходного сигнала для предотвращения подачи сигнала управления, который превышает физические возможности исполнительного механизма.
// Встроенный в дифференциальную составляющую фильтр низких частот, для устранения дифференцирования шумов измерения.
// Фильтр шума- дискретное апериодическое звено первого порядка.
// Переключение режимов управления РУЧНОЙ / АВТОМАТ.
// Безударный переход из ручного режима в автоматический.
// Безударный переход из автоматического режима в ручной.
// Принудительное обнуление интегратора интегральной составляющей при Ki=0.
// Принудительное обнуление интегратора дифференциальной составляющей при Kd=0.
// Численное интегрирование методом прямоугольников.
// Нет ни одной операции деления.
// Протестировано на PLC SIEMENS SIMATIC S7-300 в блоке OB35 (Ts=0,1c).

// Известные недостатки ПИД- регулятора.
//
// При переходе из автоматического режима в ручной безударный переход отсутствует, если не подвязать через внешний тег переменную выхода OP к входу MANUAL.
// Безударный переход работает только если Ki не равен нолю.
// Необходимо следить, чтобы верхняя граница зоны нечувствительности была больше или равна нижней.
// Необходимо следить, чтобы верхняя граница амплитудного ограничителя была больше нижней.
// Необходимо следить, чтобы коэффициент Kdf был больше ноля.
// Необходимо помнить, что коэффициент Kdf обратно пропорционален постоянной времени фильтра.
// Прямоугольный интегратор имеет меньшую точность, чем трапецеидальный.
// Арифметика с плавающей точкой работает медленнее целочисленной.
// Точность вычислений не контролируется.
// Необходимо следить при наладке, чтобы не было слишком малых измеренных значений и интегральной составляющей при большом выходном сигнале.
// Иначе интегрирование может остановиться и за того, что мы пытаемся прибавить очень маленькое число к очень большому, а точность типа данных не позволяет это сделать.
// ПИ- закон регулирования с апериодическим объектом управления по определе-нию не может безошибочно «отрабатывать» линейно нарастающий сигнал задания.
// Сигналы SP и PV нужно ограничивать вне регулятора,
// диаппазоны ограничения для SP и PV должны быть одинаковыми,
// если этого не сделать то будут проблемы в автоматическом режиме работы при активной работе амплитудного ограничителя,
// например SP=90% PV=110% ожидаем Out=0%, но при изменении PV возрастает Out хотя должно быть Out=0%.

//Структура регулятора:
//
// Ошибка регулирования.
//                   +---+
//                   |   |
//        Setpoint->-|+  |
//                   |   |->-Er
// ProcessVariable->-|-  |
//                   |   |
//                   +---+
//
// Зона нечувствительности к ошибке.
//          DBmax
//      +-----------+
//      |           |
//      |       /   |
//      |      /    |
//      |      |    |
// Er->-|   +--+    |->-Er
//      |   |       |
//      |  /        |
//      | /         |
//      |           |
//      +-----------+
//          DBmin
//
// Пропорциональная составляющая.
//      +---+
//      |   |
// Er->-|   |
//      | * |->-Ppart
// Kp->-|   |
//      |   |
//      +---+
//
// Интегральная составляющая.
//      +---+   +----------+
//      |   |   |          |
// Er->-|   |   |  Z * Ts  |
//      | * |->-| -------- |->-Ipart
// Kp->-|   |   |  Z - 1   |
//      |   |   |          |
//      +---+   +----------+
//
// Дифференциальная составляющая.
//                        +---+
//                        |   |
//      +---+       Kdf->-|   |
//      |   |     +---+   |   |
// Er->-|   |     |   |   | * |----------------+-->-Dpart
//      | * |--->-|+  |   |   |                |
// Kd->-|   |     |   |->-|   |                |
//      |   | +->-|-  |   |   | +----------+   |
//      +---+ |   |   |   +---+ |          |   |
//            |   +---+         |  Z * Ts  |   |
//            +-----------------| -------- |-<-+
//                              |  Z - 1   |
//                              |          |
//                              +----------+
//
// Сигнал управления.
//         +---+
//         |   |
// Ppart->-|+  |
//         |   |
// Ipart->-|+  |->-Auto
//         |   |
// Dpart->-|+  |
//         |   |
//         +---+
//
// Переключение режима работы "Ручной / Автоматический".
//          +-----+
//          |     |
// Auto--->-|-+   |
//          |  \  |
//          |   +-|->-Sw
//          |     |
// Manual->-|-+   |
//          |     |
//          +-----+
//             |
// ManOn-->----+
//
// Амплитудный ограничитель.
//         OutMax
//      +-----------+
//      |           |
//      |       +-- |
//      |      /    |
// Sw->-|     /     |->-Out
//      |    /      |
//      | --+       |
//      |           |
//      +-----------+
//         OutMin


// Реакция разомкнутого контура регулятора на единичное спупечатое воздействие по каналу управления.
//
// ^ Er
// |
// 1************ Er(t) = 1
// |            
// 0---1---2---3---> t[s]
//
// ^ Ppart
// |
// |************ Ppart(t) = Kp * Er
// |            
// |            
// |            
// 0---1---2---3---> t[s]
//
// ^ Ipart
// |
// 3           * Ipart(t) = Ki * t
// |         *  
// 2       *    
// |     *      
// 1   *        
// | *          
// 0---1---2---3---> t[s]
//
// ^ Dpart
// |
// 1*            Dpart(t) = Kd * (0 - exp(-(t/Tf)))
// | *          
// |  *        
// |    *       
// |      *     
// 0---------***---> t[s]
//
// ^ Out
// |
// |                               *
// |                             *
// |                           *
// |                         *
// |                       *
// |                     *
// |*                  *
// | *               *
// |  *            *
// |    *        *
// |      *    *
// |        **
// |
// |
// 0------------------------------------> t[s]

// Интерфейс пользователя SCADA, HMI.
//
// Шрифт: Arial 12pt
// Цвет фона: R212 G208 B200
// Цвет тени: R169 G169 B169
// Цвет "Заданное значение"  : R0 G0 B255
// Цвет "Измеренное значение": R0 G176 B80
// Цвет "Сигнал управления"  : R255 G0 B0
//
// +------------------------------------------------------+---+
// | 1PIRCA1 ПИД Регулятор давления воды.                 | X |
// +------------------------------------------------------+---+
// | +-------+ +--------------------------------------------+ |
// | | 10.00 | | Заданное значение           0...10 [Bar] B | |
// | +-------+ +--------------------------------------------+ |
// | +-------+ +--------------------------------------------+ |
// | | 10.01 | | Измеренное значение         0...10 [Bar] G | |
// | +-------+ +--------------------------------------------+ |
// | +-------+ +--------------------------------------------+ |
// | |  50.0 | | Сигнал управления           0...100 [Hz] R | |
// | +-------+ +--------------------------------------------+ |
// | +---------+ +--------+ +------+ +--------+ +-----------+ |
// | | АВТОМАТ | | РУЧНОЙ | | СТОП | | ТРЕНДЫ | | НАСТРОЙКИ | |
// | +---------+ +--------+ +------+ +--------+ +-----------+ |
// | Код ошибки: 0 (OK).                                      |
// +----------------------------------------------------------+
//
// +-------------------------------------------+---+
// | 1PIRCA1 Тренды.                           | X |
// +-------------------------------------------+---+
// | Заданное значение   [Bar] B                   |
// | Измеренное значение [Bar] G                   |
// | Сигнал управления   [Hz ] R                   |
// |   ^                                           |
// |   |                                           |
// |100+                                           |
// |   |                                           |
// |   |            **********************         |
// |   |           *                               |
// |   |          *                                |
// | 50+    +----*------------------------         |
// |   |    |   *  +++++++++++++++++++++++         |
// |   |    |  *  +                                |
// |   |    | *  +                                 |
// |   |    |*  +                                  |
// |   +----+----+----+----+----+----+----+-> t[s] |
// |   0   10   20   30   40   50   60   70        |
// +-----------------------------------------------+
//
// +-------------------------------------------+---+
// | 1PIRCA1 Настройки ПИД регулятора.         | X |
// +-------------------------------------------+---+
// | +-------+ +---------------------------------+ |
// | | 0.001 | | Kp                              | |
// | +-------+ +---------------------------------+ |
// | +-------+ +---------------------------------+ |
// | | 0.001 | | Ki                              | |
// | +-------+ +---------------------------------+ |
// | +-------+ +---------------------------------+ |
// | |   0.0 | | Kd                              | |
// | +-------+ +---------------------------------+ |
// | +-------+ +---------------------------------+ |
// | | 1.000 | | Kdf                             | |
// | +-------+ +---------------------------------+ |
// | +-------+ +---------------------------------+ |
// | | 0.001 | | DBmax                           | |
// | +-------+ +---------------------------------+ |
// | +-------+ +---------------------------------+ |
// | |-0.001 | | DBmin                           | |
// | +-------+ +---------------------------------+ |
// | +-------+ +---------------------------------+ |
// | | 100.0 | | OutMax                          | |
// | +-------+ +---------------------------------+ |
// | +-------+ +---------------------------------+ |
// | |   0.0 | | OutMin                          | |
// | +-------+ +---------------------------------+ |
// +-----------------------------------------------+

//  +---------+
//  | GNU GPL |
//  +---------+
//  |
//  |
//  .= .-_-. =.
// ((_/)o o(\_))
//  `-'(. .)`-'
//  |/| \_/ |\
//  ( |     | )
//  /"\_____/"\
//  \__)   (__/
// @COPYLEFT ALL WRONGS RESERVED :)
// Author: VA
// Contacts: DIY.PLC.314@gmail.com
// License: GNU GPL v2
