FUNCTION_BLOCK "FbPIDcontrol"
TITLE = 'ПИД-регулятор.'

{ S7_read_back        := 'true' ;
  S7_blockview        := 'big'  }

AUTHOR  : VA
FAMILY  : LIB_PLC
VERSION : '3.6'

VAR_INPUT // Входные переменные, сохраняемые.
PV     :REAL := 0.0   ; // Измеренное значение регулируемого параметра.
SP     :REAL := 0.0   ; // Заданное значение регулируемого параметра.
Kp     :REAL := 0.01  ; // Коэффициент усиления пропорциональный.
Ki     :REAL := 0.01  ; // Коэффициент усиления интегральный.
Kd     :REAL := 0.0   ; // Коэффициент усиления дифференциальный.
Kdf    :REAL := 1.0   ; // Коэффициент фильтрации дифференциальный Kdf=1/Tdf.
ERMAX  :REAL :=  0.001; // Зона нечувствительности к ошибке регулирования, максимум.
ERMIN  :REAL := -0.001; // Зона нечувствительности к ошибке регулирования, минимум.
MVMAX  :REAL := 100.0 ; // Максимальное значение сигнала управления.
MVMIN  :REAL := 0.0   ; // Минимальное значение сигнала управления.
Ts     :REAL := 0.1   ; // Шаг дискретизации по времени в секундах.
Manual :REAL := 25.0  ; // Сигнал управления в ручном режиме работы.
OnMan  :BOOL := FALSE ; // Включить ручной режим работы регулятора.
END_VAR

VAR_OUTPUT // Выходные переменные, сохраняемые.
MV :REAL := 0.0; // Сигнал управления на исполнительный механизм.
END_VAR

VAR // Внутренние переменные, сохраняемые.
ER        :REAL := 0.0; // Ошибка регулирования.
Ppart     :REAL := 0.0; // Пропорциональная составляющая регулятора.
Ipart     :REAL := 0.0; // Интегратор интегральной составляющей регулятора.
Dpart     :REAL := 0.0; // Дифференциальная составляющая регулятора.
Dintegral :REAL := 0.0; // Интегратор дифференциальной составляющей регулятора.
END_VAR

VAR_TEMP // Временные переменные, не сохраняемые.
Auto :REAL; // Выход ПИД- алгоритма и вход переключателя режимов работы.
Sw   :REAL; // Выход с переключателя режимов работы.
END_VAR

BEGIN

// ПИД-регулятор.
//         DbPIDcontrol
//    +--------------------+
//    |    FbPIDcontrol    |
// ->-|PV                MV|->-
//   -|SP                  |
//   -|Kp                  |
//   -|Ki                  |
//   -|Kd                  |
//   -|Kdf                 |
//   -|ERMAX               |
//   -|ERMIN               |
//   -|MVMAX               |
//   -|MVMIN               |
//   -|Ts                  |
//   -|Manual              |
//   -|OnMan               |
//    +--------------------+

// Ошибка регулирования.
ER := SP - PV;

// Зона нечувствительности к ошибке.
IF ((ERMIN < ER) AND (ER < ERMAX)) THEN
ER := 0.0;
END_IF;

// Пропорциональная составляющая.
Ppart := Kp * ER;

// Интегральная составляющая.
IF (Ki = 0.0) THEN
Ipart := 0.0;
ELSE
Ipart := Ipart + Ki * ER * Ts;
END_IF;

// Дифференциальная составляющая.
IF (Kd = 0.0) THEN
Dpart := 0.0;
Dintegral := 0.0;
ELSE
Dpart := (ER * Kd - Dintegral) * Kdf;
Dintegral := Dintegral + Dpart * Ts;
END_IF;

// Сигнал управления.
Auto := Ppart + Ipart + Dpart;

// Переключение режима работы "Ручной / Автоматический".
IF (OnMan) THEN
Sw := Manual;
ELSE
Sw := Auto;
END_IF;

// Амплитудный ограничитель- максимум.
IF (Sw >= MVMAX) THEN
MV := MVMAX;
// Ограничение интегральной составляющей методом обратного вычисления.
Ipart := MV - (Ppart + Dpart);
END_IF;

// Амплитудный ограничитель- минимум.
IF (Sw <= MVMIN) THEN
MV := MVMIN;
// Ограничение интегральной составляющей методом обратного вычисления.
Ipart := MV - (Ppart + Dpart);
END_IF;

// Амплитудный ограничитель- без ограничений.
IF ((MVMIN < Sw) AND (Sw < MVMAX)) THEN
MV := Sw;
  IF (OnMan) THEN
  // Ограничение интегральной составляющей методом обратного вычисления.
  Ipart := MV - (Ppart + Dpart);
  END_IF;
END_IF;

// Передаточная функция регулятора (при Ts -> 0):
//
//          MV(s)               1              s
// W(s) = -------- = Kp + Ki * --- + Kd * -------------
//          ER(s)               s          Tdf * s + 1
//
// ER(s) = SP(s) - PV(s).
// Tdf = 1 / Kdf

// Передаточная функция интегратора:
//
//         Ts * z
// W(z) = --------
//         z - 1
//
// Численное интегрирование методом прямоугольников.
// y = y + x * Ts;

// Перечень сокращений.
//
// Fb          - Function block       - Функциональный блок.
// Db          - Data block           - Блок данных.
// PIDcontrol  - PID Controller       - ПИД- регулятор (пропорционально интегрально дифференциальный регулятор).
// SP          - Set Point            - Заданное значение.
// PV          - Process Variable     - Измеренное значение.
// MV          - Manipulated Variable - Сигнал управления.
// ER          - Error                - Ошибка регулирования, рассогласование.
// ERMAX       - Erorr maximum        - Максимум зоны нечувствительности.
// ERMIN       - Erorr minimum        - Минимум зоны нечувствительности.
// MVMAX       - MV maximum           - Максимум сигнала управления.
// MVMIN       - MV minimum           - Минимум сигнала управления.
// Ts          - Sample Time          - Шаг дискретизации по времени.
// ManOn       - Manual on            - Включить ручной режим управления.
// Sw          - Switch               - Переключатель.
// CW          - Control Word         - Слово (ui16 бит) управления (по битам).
// SW          - Status Word          - Слово (ui16 бит) состояния (по битам).
// EW          - Error Word           - Слово (ui16 бит) ошибок (по битам).
// Альтернативные варианты имен.
// CV          - Control Variable     - Измеренное значение.
// PV          - Process Value        - Измеренное значение.
// CS          - Control Signal       - Сигнал управления.
// MV          - Manipulated Value    - Сигнал управления.
// OP          - Output Power         - Сигнал управления.

// Характеристики ПИД-регулятора.
//
// Ограничение интегральной составляющей методом обратного вычисления.
// Зона нечувствительности к ошибке регулирования для устранения небольших колебаний регулятора в установившемся режиме работы.
// Амплитудное ограничение выходного сигнала для предотвращения подачи сигнала управления, который превышает физические возможности исполнительного механизма.
// Встроенный в дифференциальную составляющую фильтр низких частот, для устранения дифференцирования шумов измерения.
// Фильтр шума- дискретное апериодическое звено первого порядка.
// Переключение режимов управления РУЧНОЙ / АВТОМАТ.
// Безударный переход из ручного режима в автоматический.
// Безударный переход из автоматического режима в ручной.
// Принудительное обнуление интегратора интегральной составляющей при Ki=0.
// Принудительное обнуление интегратора дифференциальной составляющей при Kd=0.
// Численное интегрирование методом прямоугольников.
// Нет ни одной операции деления.
// Протестировано на PLC SIEMENS SIMATIC S7-300 в блоке OB35 (Ts=0,1c).

// Известные недостатки ПИД-регулятора.
//
// При переходе из автоматического режима в ручной безударный переход отсутствует, если не подвязать через внешний тег переменную выхода MV к входу MANUAL.
// Безударный переход работает только если Ki не равен нолю.
// Необходимо следить, чтобы верхняя граница зоны нечувствительности была больше или равна нижней.
// Необходимо следить, чтобы верхняя граница амплитудного ограничителя была больше нижней.
// Необходимо следить, чтобы коэффициент Kdf был больше ноля.
// Необходимо помнить, что коэффициент Kdf обратно пропорционален постоянной времени фильтра.
// Прямоугольный интегратор имеет меньшую точность, чем трапецеидальный.
// Арифметика с плавающей точкой работает медленнее целочисленной.
// Точность вычислений не контролируется.
// Необходимо следить при наладке, чтобы не было слишком малых измеренных значений и интегральной составляющей при большом выходном сигнале.
// Иначе интегрирование может остановиться и за того, что мы пытаемся прибавить очень маленькое число к очень большому, а точность типа данных не позволяет это сделать.
// ПИ- закон регулирования с апериодическим объектом управления по определе-нию не может безошибочно «отрабатывать» линейно нарастающий сигнал задания.
// Сигналы SP и PV нужно ограничивать вне регулятора,
// диаппазоны ограничения для SP и PV должны быть одинаковыми,
// если этого не сделать то будут проблемы в автоматическом режиме работы при активной работе амплитудного ограничителя,
// например SP=90% PV=110% ожидаем MV=0%, но при изменении PV возрастает MV хотя должно быть MV=0%.

// Полезная информация по теме:
// https://www.reallab.ru/bookasutp/5-pid-regulyatori/

// Структура регулятора:
//
// Ошибка регулирования.
//      +---+
//      |   |
// SP->-|+  |
//      |   |->-ER
// PV->-|-  |
//      |   |
//      +---+
//
// Зона нечувствительности к ошибке.
//          ERMAX
//      +-----------+
//      |           |
//      |       /   |
//      |      /    |
//      |      |    |
// ER->-|   +--+    |->-ER
//      |   |       |
//      |  /        |
//      | /         |
//      |           |
//      +-----------+
//          ERMIN
//
// Пропорциональная составляющая.
//      +---+
//      |   |
// ER->-|   |
//      | * |->-Ppart
// Kp->-|   |
//      |   |
//      +---+
//
// Интегральная составляющая.
//      +---+   +----------+
//      |   |   |          |
// ER->-|   |   |  Z * Ts  |
//      | * |->-| -------- |->-Ipart
// Kp->-|   |   |  Z - 1   |
//      |   |   |          |
//      +---+   +----------+
//
// Дифференциальная составляющая.
//                        +---+
//                        |   |
//      +---+       Kdf->-|   |
//      |   |     +---+   |   |
// ER->-|   |     |   |   | * |----------------+-->-Dpart
//      | * |--->-|+  |   |   |                |
// Kd->-|   |     |   |->-|   |                |
//      |   | +->-|-  |   |   | +----------+   |
//      +---+ |   |   |   +---+ |          |   |
//            |   +---+         |  Z * Ts  |   |
//            +-----------------| -------- |-<-+
//                              |  Z - 1   |
//                              |          |
//                              +----------+
//
// Сигнал управления.
//         +---+
//         |   |
// Ppart->-|+  |
//         |   |
// Ipart->-|+  |->-Auto
//         |   |
// Dpart->-|+  |
//         |   |
//         +---+
//
// Переключение режима работы "Ручной / Автоматический".
//          +-----+
//          |     |
// Auto--->-|-+   |
//          |  \  |
//          |   +-|->-Sw
//          |     |
// Manual->-|-+   |
//          |     |
//          +-----+
//             |
// OnMan-->----+
//
// Амплитудный ограничитель.
//          MVMAX
//      +-----------+
//      |           |
//      |       +-- |
//      |      /    |
// Sw->-|     /     |->-MV
//      |    /      |
//      | --+       |
//      |           |
//      +-----------+
//          MVMIN


// Реакция разомкнутого контура регулятора на единичное спупечатое воздействие по каналу управления.
//
// ^ ER
// |
// 1************ ER(t) = 1
// |            
// 0---1---2---3---> t[s]
//
// ^ Ppart
// |
// |************ Ppart(t) = Kp * ER
// |            
// |            
// |            
// 0---1---2---3---> t[s]
//
// ^ Ipart
// |
// 3           * Ipart(t) = Ki * t
// |         *  
// 2       *    
// |     *      
// 1   *        
// | *          
// 0---1---2---3---> t[s]
//
// ^ Dpart
// |
// 1*            Dpart(t) = Kd * (0 - exp(-(t/Tf)))
// | *          
// |  *        
// |    *       
// |      *     
// 0---------***---> t[s]
//
// ^ MV
// |
// |                               *
// |                             *
// |                           *
// |                         *
// |                       *
// |                     *
// |*                  *
// | *               *
// |  *            *
// |    *        *
// |      *    *
// |        **
// |
// |
// 0------------------------------------> t[s]

// Интерфейс пользователя SCADA, HMI.
//
// Шрифт: Arial 12pt
// Цвет фона: R212 G208 B200
// Цвет тени: R169 G169 B169
// Цвет "Заданное значение"  : R0 G0 B255
// Цвет "Измеренное значение": R0 G176 B80
// Цвет "Сигнал управления"  : R255 G0 B0
//
// +------------------------------------------------------+---+
// | 1PIRCA1 ПИД Регулятор давления воды.                 | X |
// +------------------------------------------------------+---+
// | +-------+ +--------------------------------------------+ |
// | | 10.00 | | Заданное значение           0...10 [Bar] B | |
// | +-------+ +--------------------------------------------+ |
// | +-------+ +--------------------------------------------+ |
// | | 10.01 | | Измеренное значение         0...10 [Bar] G | |
// | +-------+ +--------------------------------------------+ |
// | +-------+ +--------------------------------------------+ |
// | |  50.0 | | Сигнал управления           0...100 [Hz] R | |
// | +-------+ +--------------------------------------------+ |
// | +---------+ +--------+ +------+ +--------+ +-----------+ |
// | | АВТОМАТ | | РУЧНОЙ | | СТОП | | ТРЕНДЫ | | НАСТРОЙКИ | |
// | +---------+ +--------+ +------+ +--------+ +-----------+ |
// | Код ошибки: 0 (OK).                                      |
// +----------------------------------------------------------+
//
// +-------------------------------------------+---+
// | 1PIRCA1 Тренды.                           | X |
// +-------------------------------------------+---+
// | Заданное значение   [Bar] B                   |
// | Измеренное значение [Bar] G                   |
// | Сигнал управления   [Hz ] R                   |
// |   ^                                           |
// |   |                                           |
// |100+                                           |
// |   |                                           |
// |   |            **********************         |
// |   |           *                               |
// |   |          *                                |
// | 50+    +----*------------------------         |
// |   |    |   *  +++++++++++++++++++++++         |
// |   |    |  *  +                                |
// |   |    | *  +                                 |
// |   |    |*  +                                  |
// |   +----+----+----+----+----+----+----+-> t[s] |
// |   0   10   20   30   40   50   60   70        |
// +-----------------------------------------------+
//
// +-------------------------------------------+---+
// | 1PIRCA1 Настройки ПИД регулятора.         | X |
// +-------------------------------------------+---+
// | +-------+ +---------------------------------+ |
// | | 0.001 | | Kp                              | |
// | +-------+ +---------------------------------+ |
// | +-------+ +---------------------------------+ |
// | | 0.001 | | Ki                              | |
// | +-------+ +---------------------------------+ |
// | +-------+ +---------------------------------+ |
// | |   0.0 | | Kd                              | |
// | +-------+ +---------------------------------+ |
// | +-------+ +---------------------------------+ |
// | | 1.000 | | Kdf                             | |
// | +-------+ +---------------------------------+ |
// | +-------+ +---------------------------------+ |
// | | 0.001 | | ERMAX                           | |
// | +-------+ +---------------------------------+ |
// | +-------+ +---------------------------------+ |
// | |-0.001 | | ERMIN                           | |
// | +-------+ +---------------------------------+ |
// | +-------+ +---------------------------------+ |
// | | 100.0 | | MVMAX                           | |
// | +-------+ +---------------------------------+ |
// | +-------+ +---------------------------------+ |
// | |   0.0 | | MVMIN                           | |
// | +-------+ +---------------------------------+ |
// +-----------------------------------------------+

// Пересчет коэффициентов регулятора "CONT_C" 
// в коэффициенты ПИД- регулятора MATLAB SIMULINK PID Controller.
//
// Передаточная функция регулятора SIMATIC FB 41 "CONT_C":
//
//         LMN(s)                   1      (TD / TM_LAG) * s
// W(s) = -------- = GAIN * (1 + ------ + -------------------)
//          ER(s)                TI * s      TM_LAG * s + 1
//
// Единицы измерения.
// ER(s) = SP_INT(s) - PV_IN(s). 0...100[%]
// TI,TD,TM_LAG [s].
// Итого получаем формулы для пересчета настроек:
// P = Kp  = GAIN
// I = Ki  = GAIN / TI
// D = Kd  = (TD / TM_LAG) * GAIN
// N = Kdf = 1 / TM_LAG
// Так же необходимо учесть что данные настройки актуальны
// только для диаппазонов входного и выходного сигнала 0...100[%].
// Время необходимо привести в секунды.

// @COPYLEFT ALL WRONGS RESERVED :)
// Author: VA
// Contacts: DIY.PLC.314@gmail.com
// Date start LIB_PLC: 2014
// License: GNU GPL-2.0-or-later
// https://www.gnu.org/licenses/old-licenses/gpl-2.0.html
// https://www.youtube.com/watch?v=n1F_MfLRlX0
// https://www.youtube.com/@DIY_PLC
// https://github.com/DIYPLC/LIB_PLC
// https://oshwlab.com/diy.plc.314/PLC_HW1_SW1
// https://3dtoday.ru/3d-models/mechanical-parts/body/korpus-na-din-reiku
// https://t.me/DIY_PLC

END_FUNCTION_BLOCK
