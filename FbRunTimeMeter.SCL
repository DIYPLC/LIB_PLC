FUNCTION_BLOCK FbRunTimeMeter //Измерение времени выполнения участка программы.
TITLE = 'Измерение времени выполнения участка программы.'

//            DbRunTimeMeter
//     +--------------------------+
//     |      FbRunTimeMeter      |
//  ->-|StartSeconds       RunTime|->-
//  ->-|StartMillisecond          |
//  ->-|StopSeconds               |
//  ->-|StopMillisecond           |
//     +--------------------------+

{//Атрибуты для PCS7.
S7_read_back := 'true' ; //CFC: Chart>Readback активен для прототипов DB.
S7_blockview := 'big'  ; //CFC: отображение блока мелнькое или большое.
//Опции компилятора SCL.
GenerateReferenceData := 'y' ; //SCL: Генерировать перекрестные ссылки.
CreateDebugInfo       := 'y' ; //SCL: Генерировать оладочную информацию.
SetOKFlag             := 'y' ; //SCL: ENO = 1
MonitorArrayLimits    := 'y' ; //SCL: Следить за границами массивов.
OptimizeObjectCode    := 'y'   //SCL: Оптимизация объектного кода.
}

//Атрибуты для STEP7.
VERSION : '2.1'
AUTHOR  : VA
FAMILY  : LIB_PLC

VAR_INPUT //Входные переменные, сохраняемые.
StartSeconds     :INT := 0; //Время в начале измерения- секунда 0...59.
StartMillisecond :INT := 0; //Время в начале измерения- миллисекунда 0...999.
StopSeconds      :INT := 0; //Время в конце измерения- секунда 0...59.
StopMillisecond  :INT := 0; //Время в конце измерения- миллисекунда 0...999.
END_VAR

VAR_OUTPUT //Выходные переменные, сохраняемые.
RunTime :INT := 0; //Измеренное время выполнения программы [мс].
END_VAR

VAR //Внутренние переменные, сохраняемые.
Time_start_ms :DINT := 0; //Метка времени в начале измерения 0...59999ms
Time_stop_ms  :DINT := 0; //Метка времени в конце измерения 0...59999ms
END_VAR

BEGIN

//Начало измерения 00s000ms...59s999ms == 0...59999ms
Time_start_ms := ( INT_TO_DINT(StartSeconds) * 1000 ) + INT_TO_DINT(StartMillisecond);

//Конец измерения 00s000ms...59s999ms == 0...59999ms.
Time_stop_ms := ( INT_TO_DINT(StopSeconds) * 1000 ) + INT_TO_DINT(StopMillisecond);

//Разность времени с учетом переполнения.
IF (Time_stop_ms >= Time_start_ms) THEN
RunTime := DINT_TO_INT(Time_stop_ms - Time_start_ms);
//Ts_ms = 7 - 2 = 5 ms
//0
//1
//2 <- Time_start_ms
//3
//4
//5
//6
//7 <- Time_stop_ms
//8
//9
ELSE
RunTime := DINT_TO_INT((60000 - Time_start_ms) + Time_stop_ms);
//Ts_ms = (60000 - 59997) + 2 = 5 ms
//59995
//59996
//59997 <- Time_start_ms
//59998
//59999
//0
//1
//2 <- Time_stop_ms
//3
//4
END_IF;

// @COPYLEFT ALL WRONGS RESERVED :)
// Author: VA
// Contacts: DIY.PLC.314@gmail.com
// Date start LIB_PLC: 2014
// License: GNU GPL-2.0-or-later
// https://www.gnu.org/licenses/old-licenses/gpl-2.0.html
// https://www.youtube.com/watch?v=n1F_MfLRlX0
// https://www.youtube.com/@DIY_PLC
// https://github.com/DIYPLC/LIB_PLC
// https://oshwlab.com/diy.plc.314/PLC_HW1_SW1
// https://3dtoday.ru/3d-models/mechanical-parts/body/korpus-na-din-reiku
// https://t.me/DIY_PLC

END_FUNCTION_BLOCK
